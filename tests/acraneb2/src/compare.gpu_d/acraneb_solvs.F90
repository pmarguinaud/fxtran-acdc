!OPTIONS XOPT(NOEVAL)
SUBROUTINE ACRANEB_SOLVS(YDPHY,KIDIA,KFDIA,KLON,KTDIA,KLEV,LDMASKS,&
! - INPUT 1D
 & PALB,PALBDIR,PCLCT,PFRSOPS_C,PFRSOPS_CUN,&
! - INPUT 2D
 & PNEB,PB1,PB2,PB3,PB4,&
! - INPUT/OUTPUT 2D
 & PA1C,PA1CUN,PA2C,PA3C,PA4C,PA5C,PA1N,PA1NUN,PA2N,PA3N,PA4N,PA5N,&
 & PFDC,PFMC,PFPC,PFPCUN,PFDN,PFMN,PFPN,PFPNUN, YDSTACK)
!$acc routine (ACRANEB_SOLVS) seq

! Purpose:
! --------
!   ACRANEB_SOLVS - Adding system solver (solar band).

! Interface:
! ----------
! INPUT:
!   KLON        - horizontal dimension of arrays
!   KTDIA       - initial index for vertical loops (usually 1)
!   KLEV        - vertical dimension of full level arrays
!   KJN         - dimension of arrays containing "daylight" intervals
!   KIIDIA      - array of indices marking start of "daylight" intervals
!   KIFDIA      - array of indices marking end of "daylight" intervals
!   KAUCR       - number of "daylight" intervals
!   PALB        - diffuse surface albedo
!   PALBDIR     - direct (parallel) surface albedo
!   PCLCT       - total cloud cover
!   PFRSOPS_C   - direct solar flux at surface, delta-scaled
!   PFRSOPS_CUN - direct solar flux at surface, delta-unscaled
!   PNEB        - cloud fraction
!   PB1         - 1st intermediate storage for cloud geometry
!   PB2         - 2nd intermediate storage for cloud geometry
!   PB3         - 3rd intermediate storage for cloud geometry
!   PB4         - 4th intermediate storage for cloud geometry
! INPUT/OUTPUT:
!   PA1C        - clearsky parallel transmissivity
!   PA1CUN      - clearsky parallel transmissivity, not delta-scaled
!   PA2C        - clearsky parallel/diffuse transmissivity
!   PA3C        - clearsky parallel/diffuse reflectivity
!   PA4C        - clearsky diffuse transmissivity
!   PA5C        - clearsky diffuse reflectivity
!   PA1N        - cloudy parallel transmissivity
!   PA1NUN      - cloudy parallel transmissivity, not delta-scaled
!   PA2N        - cloudy parallel/diffuse transmissivity
!   PA3N        - cloudy parallel/diffuse reflectivity
!   PA4N        - cloudy diffuse transmissivity
!   PA5N        - cloudy diffuse reflectivity
!   PFPC        - right hand side term for delta-scaled clearsky parallel flux
!   PFPCUN      - right hand side term for true clearsky parallel flux
!   PFPN        - right hand side term for delta-scaled cloudy parallel flux
!   PFPNUN      - right hand side term for true cloudy parallel flux
!   PFDC        - right hand side term for clearsky downwards diffusive flux
!   PFDN        - right hand side term for cloudy downwards difffusive flux
!   PFMC        - right hand side term for clearsky upwards difffusive flux
!   PFMN        - right hand side term for cloudy upwards difffusive flux

! Externals:
! ----------

! Method:
! -------

! Reference:
! ----------

! Author:
! -------
!   1989-12, J.-F. Geleyn (original ACRANEB)

! Modifications:
! --------------
!   2009-10, T. Kral
!   Externalized from ACRANEB.

!   2013-11, J. Masek
!   Backphasing to cy38t1.

!   2016-04, J. Masek
!   Computation of true direct solar flux.

!   2016-09, J. Masek
!   Proper naming of variables containing delta-unscaled values,
!   changes needed for sunshine duration.
! End Modifications
!-------------------------------------------------------------------------------

USE PARKIND1  ,ONLY : JPIM    ,JPRB
USE YOMPHY    ,ONLY : TPHY
USE STACK_MOD
#include "stack.h"

IMPLICIT NONE

TYPE(TPHY)        ,INTENT(INOUT):: YDPHY
INTEGER(KIND=JPIM),INTENT(IN)  :: KIDIA
INTEGER(KIND=JPIM),INTENT(IN)  :: KFDIA
INTEGER(KIND=JPIM),INTENT(IN)  :: KLON 
INTEGER(KIND=JPIM),INTENT(IN)  :: KTDIA 
INTEGER(KIND=JPIM),INTENT(IN)  :: KLEV 
LOGICAL,          INTENT(IN)  :: LDMASKS(KLON)

REAL(KIND=JPRB),INTENT(IN)     :: PALB(KLON)
REAL(KIND=JPRB),INTENT(IN)     :: PALBDIR(KLON)
REAL(KIND=JPRB),INTENT(IN)     :: PCLCT(KLON)
REAL(KIND=JPRB),INTENT(IN)     :: PFRSOPS_C(KLON)
REAL(KIND=JPRB),INTENT(IN)     :: PFRSOPS_CUN(KLON)
REAL(KIND=JPRB),INTENT(IN)     :: PNEB(KLON,KLEV)
REAL(KIND=JPRB),INTENT(IN)     :: PB1(KLON,KLEV)
REAL(KIND=JPRB),INTENT(IN)     :: PB2(KLON,KLEV)
REAL(KIND=JPRB),INTENT(IN)     :: PB3(KLON,KLEV)
REAL(KIND=JPRB),INTENT(IN)     :: PB4(KLON,KLEV)
REAL(KIND=JPRB),INTENT(INOUT)  :: PA1C(KLON,KLEV)
REAL(KIND=JPRB),INTENT(INOUT)  :: PA1CUN(KLON,KLEV)
REAL(KIND=JPRB),INTENT(INOUT)  :: PA2C(KLON,KLEV)
REAL(KIND=JPRB),INTENT(INOUT)  :: PA3C(KLON,KLEV)
REAL(KIND=JPRB),INTENT(INOUT)  :: PA4C(KLON,KLEV)
REAL(KIND=JPRB),INTENT(INOUT)  :: PA5C(KLON,KLEV)
REAL(KIND=JPRB),INTENT(INOUT)  :: PA1N(KLON,KLEV)
REAL(KIND=JPRB),INTENT(INOUT)  :: PA1NUN(KLON,KLEV)
REAL(KIND=JPRB),INTENT(INOUT)  :: PA2N(KLON,KLEV)
REAL(KIND=JPRB),INTENT(INOUT)  :: PA3N(KLON,KLEV)
REAL(KIND=JPRB),INTENT(INOUT)  :: PA4N(KLON,KLEV)
REAL(KIND=JPRB),INTENT(INOUT)  :: PA5N(KLON,KLEV)
REAL(KIND=JPRB),INTENT(INOUT)  :: PFDC(KLON,0:KLEV)
REAL(KIND=JPRB),INTENT(INOUT)  :: PFMC(KLON,0:KLEV)
REAL(KIND=JPRB),INTENT(INOUT)  :: PFPC(KLON,0:KLEV)
REAL(KIND=JPRB),INTENT(INOUT)  :: PFPCUN(KLON,0:KLEV)
REAL(KIND=JPRB),INTENT(INOUT)  :: PFDN(KLON,0:KLEV)
REAL(KIND=JPRB),INTENT(INOUT)  :: PFMN(KLON,0:KLEV)
REAL(KIND=JPRB),INTENT(INOUT)  :: PFPN(KLON,0:KLEV)
REAL(KIND=JPRB),INTENT(INOUT)  :: PFPNUN(KLON,0:KLEV)
TYPE(STACK) :: YDSTACK
TYPE(STACK) :: YLSTACK

#include "abor1.intfb.h"

!     LOCAL REAL SCALARS
REAL(KIND=JPRB) :: ZAL,ZALP,ZTD1,ZTD2,ZTD3,ZTD4,ZTD5,ZTD6,ZTD7,&
                 & ZTDS1,ZTDS2,ZTDS3,ZTUS1,ZFPC_TRUE

!     LOCAL REAL 1D ARRAYS
REAL(KIND=JPRB) :: ZAL1,ZAL2,ZBE1,ZBE2,&
                 & ZDE1,ZDE2,ZGA1,ZGA2

!     LOCAL REAL 2D ARRAYS
temp (REAL(KIND=JPRB), ZTU1, (KLON,KLEV))
temp (REAL(KIND=JPRB), ZTU2, (KLON,KLEV))
temp (REAL(KIND=JPRB), ZTU3, (KLON,KLEV))
temp (REAL(KIND=JPRB), ZTU4, (KLON,KLEV))
temp (REAL(KIND=JPRB), ZTU5, (KLON,KLEV))
temp (REAL(KIND=JPRB), ZTU6, (KLON,KLEV))
temp (REAL(KIND=JPRB), ZTU7, (KLON,KLEV))
temp (REAL(KIND=JPRB), ZTU8, (KLON,KLEV))
temp (REAL(KIND=JPRB), ZTU9, (KLON,KLEV))

!     LOCAL INTEGER SCALARS
INTEGER(KIND=JPIM) :: JLEV, JLON, JN

YLSTACK = YDSTACK

alloc (ZTU1)
alloc (ZTU2)
alloc (ZTU3)
alloc (ZTU4)
alloc (ZTU5)
alloc (ZTU6)
alloc (ZTU7)
alloc (ZTU8)
alloc (ZTU9)



JLON = KIDIA


! - TEMPORAIRE(S) 1D

! ZAL1      : WEIGHT OF THE M OUTGOING C FLUX IN THE M INGOING C FLUX.
! ZAL2      : WEIGHT OF THE D OUTGOING C FLUX IN THE D INGOING C FLUX.
! ZBE1      : WEIGHT OF THE M OUTGOING C FLUX IN THE M INGOING N FLUX.
! ZBE2      : WEIGHT OF THE D OUTGOING C FLUX IN THE D INGOING N FLUX.
! ZGA1      : WEIGHT OF THE M OUTGOING N FLUX IN THE M INGOING C FLUX.
! ZGA2      : WEIGHT OF THE D OUTGOING N FLUX IN THE D INGOING C FLUX.
! ZDE1      : WEIGHT OF THE M OUTGOING N FLUX IN THE M INGOING N FLUX.
! ZDE2      : WEIGHT OF THE D OUTGOING N FLUX IN THE D INGOING N FLUX.

! - TEMPORAIRE(S) 2D (1:KLEV)

! ZTU1      : 1ST SUPERDIAGONAL COEFF. FOR THE ADDING SYSTEM.
! ZTU2      : 2ND SUPERDIAGONAL COEFF. FOR THE ADDING SYSTEM.
! ZTU3      : 3RD SUPERDIAGONAL COEFF. FOR THE ADDING SYSTEM.
! ZTU4      : 4TH SUPERDIAGONAL COEFF. FOR THE ADDING SYSTEM.
! ZTU5      : 5TH SUPERDIAGONAL COEFF. FOR THE ADDING SYSTEM.
! ZTU6      : 6TH SUPERDIAGONAL COEFF. FOR THE ADDING SYSTEM.
! ZTU7      : 7TH SUPERDIAGONAL COEFF. FOR THE ADDING SYSTEM.
! ZTU8      : 8TH SUPERDIAGONAL COEFF. FOR THE ADDING SYSTEM.
! ZTU9      : 9TH SUPERDIAGONAL COEFF. FOR THE ADDING SYSTEM.

!     I.1 - FIRST LAYER, ELIMINATION (EASY).
!-------------------------------------------------------------------------------


	!IF ( LDMASKS(JLON) ) THEN
    IF (YDPHY%LRNUMX) THEN
      ZAL1=PB2(JLON,KTDIA)
      ZAL2=PB1(JLON,KTDIA)
      ZBE1=1._JPRB-PB2(JLON,KTDIA)
      ZBE2=1._JPRB-PB1(JLON,KTDIA)
      ZGA1=1._JPRB-PB4(JLON,KTDIA)
      ZGA2=1._JPRB-PB3(JLON,KTDIA)
      ZDE1=PB4(JLON,KTDIA)
      ZDE2=PB3(JLON,KTDIA)
    ELSE
      PA1C(JLON,KTDIA)=PA1C(JLON,KTDIA)*PB1(JLON,KTDIA)&
       & +PA1N(JLON,KTDIA)*PNEB(JLON,KTDIA)
      PA1CUN(JLON,KTDIA)=PA1CUN(JLON,KTDIA)*PB1(JLON,KTDIA)&
       & +PA1NUN(JLON,KTDIA)*PNEB(JLON,KTDIA)
      PA2C(JLON,KTDIA)=PA2C(JLON,KTDIA)*PB1(JLON,KTDIA)&
       & +PA2N(JLON,KTDIA)*PNEB(JLON,KTDIA)
      PA3C(JLON,KTDIA)=PA3C(JLON,KTDIA)*PB1(JLON,KTDIA)&
       & +PA3N(JLON,KTDIA)*PNEB(JLON,KTDIA)
      PA4C(JLON,KTDIA)=PA4C(JLON,KTDIA)*PB1(JLON,KTDIA)&
       & +PA4N(JLON,KTDIA)*PNEB(JLON,KTDIA)
      PA5C(JLON,KTDIA)=PA5C(JLON,KTDIA)*PB1(JLON,KTDIA)&
       & +PA5N(JLON,KTDIA)*PNEB(JLON,KTDIA)
    ENDIF

    IF (YDPHY%LRNUMX) THEN
      PFMC(JLON,KTDIA-1)=PA3C(JLON,KTDIA)*ZAL2*PFPC(JLON,KTDIA-1)
      PFPC(JLON,KTDIA)=PA1C(JLON,KTDIA)*ZAL2*PFPC(JLON,KTDIA-1)
      PFPCUN(JLON,KTDIA)=PA1CUN(JLON,KTDIA)*ZAL2*PFPCUN(JLON,KTDIA-1)
      PFDC(JLON,KTDIA)=PA2C(JLON,KTDIA)*ZAL2*PFPC(JLON,KTDIA-1)
      PFMN(JLON,KTDIA-1)=PA3N(JLON,KTDIA)*ZBE2*PFPC(JLON,KTDIA-1)
      PFPN(JLON,KTDIA)=PA1N(JLON,KTDIA)*ZBE2*PFPC(JLON,KTDIA-1)
      PFPNUN(JLON,KTDIA)=PA1NUN(JLON,KTDIA)*ZBE2*PFPCUN(JLON,KTDIA-1)
      PFDN(JLON,KTDIA)=PA2N(JLON,KTDIA)*ZBE2*PFPC(JLON,KTDIA-1)
      ZTU1(JLON,KTDIA)=0._JPRB
      ZTU2(JLON,KTDIA)=ZAL1*PA4C(JLON,KTDIA)
      ZTU3(JLON,KTDIA)=ZGA1*PA4C(JLON,KTDIA)
      ZTU4(JLON,KTDIA)=ZBE1*PA4N(JLON,KTDIA)
      ZTU5(JLON,KTDIA)=ZDE1*PA4N(JLON,KTDIA)
      ZTU6(JLON,KTDIA)=ZAL1*PA5C(JLON,KTDIA)
      ZTU7(JLON,KTDIA)=ZGA1*PA5C(JLON,KTDIA)
      ZTU8(JLON,KTDIA)=ZBE1*PA5N(JLON,KTDIA)
      ZTU9(JLON,KTDIA)=ZDE1*PA5N(JLON,KTDIA)
    ELSE
      PFMC(JLON,KTDIA-1)=PA3C(JLON,KTDIA)*PFPC(JLON,KTDIA-1)
      PFPC(JLON,KTDIA)=PA1C(JLON,KTDIA)*PFPC(JLON,KTDIA-1)
      PFPCUN(JLON,KTDIA)=PA1CUN(JLON,KTDIA)*PFPCUN(JLON,KTDIA-1)
      PFDC(JLON,KTDIA)=PA2C(JLON,KTDIA)*PFPC(JLON,KTDIA-1)
      ZTU2(JLON,KTDIA)=PA4C(JLON,KTDIA)
      ZTU6(JLON,KTDIA)=PA5C(JLON,KTDIA)
    ENDIF

  !ENDIF


! I.2 - LOOP OVER THE LAYERS, PRELIMINARY COMPUTATIONS AND THEN ELIMINATION.
!-------------------------------------------------------------------------------

DO JLEV=KTDIA+1,KLEV

	
		!IF ( LDMASKS(JLON) ) THEN

      IF (YDPHY%LRNUMX) THEN
        ZAL1=PB2(JLON,JLEV)
        ZAL2=PB1(JLON,JLEV)
        ZBE1=1._JPRB-PB2(JLON,JLEV)
        ZBE2=1._JPRB-PB1(JLON,JLEV)
        ZGA1=1._JPRB-PB4(JLON,JLEV)
        ZGA2=1._JPRB-PB3(JLON,JLEV)
        ZDE1=PB4(JLON,JLEV)
        ZDE2=PB3(JLON,JLEV)
      ELSE
        PA1C(JLON,JLEV)=PA1C(JLON,JLEV)*PB1(JLON,JLEV)&
         & +PA1N(JLON,JLEV)*PNEB(JLON,JLEV)
        PA1CUN(JLON,JLEV)=PA1CUN(JLON,JLEV)*PB1(JLON,JLEV)&
         & +PA1NUN(JLON,JLEV)*PNEB(JLON,JLEV)
        PA2C(JLON,JLEV)=PA2C(JLON,JLEV)*PB1(JLON,JLEV)&
         & +PA2N(JLON,JLEV)*PNEB(JLON,JLEV)
        PA3C(JLON,JLEV)=PA3C(JLON,JLEV)*PB1(JLON,JLEV)&
         & +PA3N(JLON,JLEV)*PNEB(JLON,JLEV)
        PA4C(JLON,JLEV)=PA4C(JLON,JLEV)*PB1(JLON,JLEV)&
         & +PA4N(JLON,JLEV)*PNEB(JLON,JLEV)
        PA5C(JLON,JLEV)=PA5C(JLON,JLEV)*PB1(JLON,JLEV)&
         & +PA5N(JLON,JLEV)*PNEB(JLON,JLEV)
      ENDIF

    !ENDIF
  

	
		!IF ( LDMASKS(JLON) ) THEN

      IF (YDPHY%LRNUMX) THEN
        ZTD1=1._JPRB/(1._JPRB-PA5C(JLON,JLEV)*(ZAL2&
         & *ZTU6(JLON,JLEV-1)+ZGA2*ZTU8(JLON,JLEV-1)))
        PFMC(JLON,JLEV-1)=ZTD1*(PA5C(JLON,JLEV)*(ZAL2&
         & *PFDC(JLON,JLEV-1)+ZGA2*PFDN(JLON,JLEV-1))&
         & +PA3C(JLON,JLEV)*(ZAL2*PFPC(JLON,JLEV-1)&
         & +ZGA2*PFPN(JLON,JLEV-1)))
        ZTU1(JLON,JLEV)=ZTD1*PA5C(JLON,JLEV)*(ZAL2&
         & *ZTU7(JLON,JLEV-1)+ZGA2*ZTU9(JLON,JLEV-1))
        ZTU2(JLON,JLEV)=(ZTD1*PA4C(JLON,JLEV))*ZAL1
        ZTU3(JLON,JLEV)=(ZTD1*PA4C(JLON,JLEV))*ZGA1
        ZTD2=PA5N(JLON,JLEV)*(ZBE2*ZTU6(JLON,JLEV-1)&
         & +ZDE2*ZTU8(JLON,JLEV-1))
        ZTD3=1._JPRB/(1._JPRB-PA5N(JLON,JLEV)*(ZBE2&
         & *ZTU7(JLON,JLEV-1)+ZDE2*ZTU9(JLON,JLEV-1))&
         & -ZTD2*ZTU1(JLON,JLEV))
        PFMN(JLON,JLEV-1)=ZTD3*(PA5N(JLON,JLEV)*(ZBE2&
         & *PFDC(JLON,JLEV-1)+ZDE2*PFDN(JLON,JLEV-1))&
         & +ZTD2*PFMC(JLON,JLEV-1)+PA3N(JLON,JLEV)*(ZBE2&
         & *PFPC(JLON,JLEV-1)+ZDE2*PFPN(JLON,JLEV-1)))
        ZTU4(JLON,JLEV)=ZTD3*(PA4N(JLON,JLEV)&
         & *ZBE1+ZTD2*ZTU2(JLON,JLEV))
        ZTU5(JLON,JLEV)=ZTD3*(PA4N(JLON,JLEV)&
         & *ZDE1+ZTD2*ZTU3(JLON,JLEV))
        PFPC(JLON,JLEV)=PA1C(JLON,JLEV)*(ZAL2&
         & *PFPC(JLON,JLEV-1)+ZGA2*PFPN(JLON,JLEV-1))
        PFPCUN(JLON,JLEV)=PA1CUN(JLON,JLEV)*(ZAL2&
         & *PFPCUN(JLON,JLEV-1)+ZGA2*PFPNUN(JLON,JLEV-1))
        PFPN(JLON,JLEV)=PA1N(JLON,JLEV)*(ZBE2&
         & *PFPC(JLON,JLEV-1)+ZDE2*PFPN(JLON,JLEV-1))
        PFPNUN(JLON,JLEV)=PA1NUN(JLON,JLEV)*(ZBE2&
         & *PFPCUN(JLON,JLEV-1)+ZDE2*PFPNUN(JLON,JLEV-1))
        ZTD4=PA4C(JLON,JLEV)*(ZAL2*ZTU6(JLON,JLEV-1)&
         & +ZGA2*ZTU8(JLON,JLEV-1))
        ZTD5=PA4C(JLON,JLEV)*(ZAL2*ZTU7(JLON,JLEV-1)&
         & +ZGA2*ZTU9(JLON,JLEV-1))
        PFDC(JLON,JLEV)=PA4C(JLON,JLEV)*(ZAL2*PFDC(JLON,JLEV-1)&
         & +ZGA2*PFDN(JLON,JLEV-1))+ZTD4*PFMC(JLON,JLEV-1)&
         & +ZTD5*PFMN(JLON,JLEV-1)+PA2C(JLON,JLEV)*(ZAL2&
         & *PFPC(JLON,JLEV-1)+ZGA2*PFPN(JLON,JLEV-1))
        ZTU6(JLON,JLEV)=PA5C(JLON,JLEV)*ZAL1&
         & +ZTD4*ZTU2(JLON,JLEV)+ZTD5*ZTU4(JLON,JLEV)
        ZTU7(JLON,JLEV)=PA5C(JLON,JLEV)*ZGA1&
         & +ZTD4*ZTU3(JLON,JLEV)+ZTD5*ZTU5(JLON,JLEV)
        ZTD6=PA4N(JLON,JLEV)*(ZBE2*ZTU6(JLON,JLEV-1)&
         & +ZDE2*ZTU8(JLON,JLEV-1))
        ZTD7=PA4N(JLON,JLEV)*(ZBE2*ZTU7(JLON,JLEV-1)&
         & +ZDE2*ZTU9(JLON,JLEV-1))
        PFDN(JLON,JLEV)=PA4N(JLON,JLEV)*(ZBE2*PFDC(JLON,JLEV-1)&
         & +ZDE2*PFDN(JLON,JLEV-1))+ZTD6 *PFMC(JLON,JLEV-1)&
         & +ZTD7*PFMN(JLON,JLEV-1)+PA2N(JLON,JLEV)*(ZBE2&
         & *PFPC(JLON,JLEV-1)+ZDE2*PFPN(JLON,JLEV-1))
        ZTU8(JLON,JLEV)=PA5N(JLON,JLEV)*ZBE1+ZTD6&
         & *ZTU2(JLON,JLEV)+ZTD7*ZTU4(JLON,JLEV)
        ZTU9(JLON,JLEV)=PA5N(JLON,JLEV)*ZDE1+ZTD6&
         & *ZTU3(JLON,JLEV)+ZTD7*ZTU5(JLON,JLEV)
      ELSE
        ZTD1=1._JPRB/(1._JPRB-PA5C(JLON,JLEV)*ZTU6(JLON,JLEV-1))
        PFMC(JLON,JLEV-1)=ZTD1*(PA5C(JLON,JLEV)*PFDC(JLON,JLEV-1)&
         & +PA3C(JLON,JLEV)*PFPC(JLON,JLEV-1))
        ZTU2(JLON,JLEV)=ZTD1*PA4C(JLON,JLEV)
        PFPC(JLON,JLEV)=PA1C(JLON,JLEV)*PFPC(JLON,JLEV-1)
        PFPCUN(JLON,JLEV)=PA1CUN(JLON,JLEV)*PFPCUN(JLON,JLEV-1)
        ZTD4=PA4C(JLON,JLEV)*ZTU6(JLON,JLEV-1)
        PFDC(JLON,JLEV)=PA4C(JLON,JLEV)*PFDC(JLON,JLEV-1)+ZTD4&
         & *PFMC(JLON,JLEV-1)+PA2C(JLON,JLEV)*PFPC(JLON,JLEV-1)
        ZTU6(JLON,JLEV)=PA5C(JLON,JLEV)+ZTD4*ZTU2(JLON,JLEV)
      ENDIF ! LRNUMX

    !ENDIF
  

ENDDO

! I.3 - SURFACE TREATMENT, ELIMINATION AND BACK-SUBSTITUTION.
!-------------------------------------------------------------------------------

! TODO: INCLUDE ALBEDO IN ZAC(N)3 & ZAC(N)5 ARRAYS, THIS REQUIRES TO 
!       INCREASE DIMENSION OF ZAC(N) TO 0:KLEV

IF ( YDPHY%LRTRUEBBC ) THEN

	
		!IF ( LDMASKS(JLON) ) THEN
      ZAL      =PALB   (JLON)
      ZALP     =PALBDIR(JLON)
      ZTDS1    =1._JPRB/(1._JPRB-ZAL*ZTU6(JLON,KLEV))
      ZFPC_TRUE=PFPCUN(JLON,KLEV)+(1._JPRB-PCLCT(JLON))*&
       & (PFRSOPS_C(JLON)-PFRSOPS_CUN(JLON))
      PFMC(JLON,KLEV)=ZTDS1*(ZAL*(PFDC(JLON,KLEV)+PFPC(JLON,KLEV)-&
       & ZFPC_TRUE)+ZALP*ZFPC_TRUE)
      IF (YDPHY%LRNUMX) THEN
        ZTUS1=ZTDS1*ZAL*ZTU7(JLON,KLEV)
        ZTDS2=ZAL*ZTU8(JLON,KLEV)
        ZTDS3=1._JPRB/(1._JPRB-ZAL*ZTU9(JLON,KLEV)-ZTDS2*ZTUS1)
        PFMN(JLON,KLEV)=ZTDS3*(ZAL*(PFDN(JLON,KLEV)+PFPN(JLON,KLEV)-&
         & PFPNUN(JLON,KLEV))+ZTDS2*PFMC(JLON,KLEV)+ZALP*PFPNUN(JLON,KLEV))
        PFMC(JLON,KLEV)=PFMC(JLON,KLEV)+ZTUS1*PFMN(JLON,KLEV)
      ENDIF
    !ENDIF
  

ELSE

	
		!IF ( LDMASKS(JLON) ) THEN
      ZAL =PALB   (JLON)
      ZALP=PALBDIR(JLON)
      ZTDS1=1._JPRB/(1._JPRB-ZAL*ZTU6(JLON,KLEV))
      PFMC(JLON,KLEV)=ZTDS1*(ZAL*PFDC(JLON,KLEV)+ZALP*PFPC(JLON,KLEV))
      IF (YDPHY%LRNUMX) THEN
        ZTUS1=ZTDS1*ZAL*ZTU7(JLON,KLEV)
        ZTDS2=ZAL*ZTU8(JLON,KLEV)
        ZTDS3=1._JPRB/(1._JPRB-ZAL*ZTU9(JLON,KLEV)-ZTDS2*ZTUS1)
        PFMN(JLON,KLEV)=ZTDS3*(ZAL*PFDN(JLON,KLEV)+&
         & ZTDS2*PFMC(JLON,KLEV)+ZALP*PFPN(JLON,KLEV))
        PFMC(JLON,KLEV)=PFMC(JLON,KLEV)+ZTUS1*PFMN(JLON,KLEV)
      ENDIF
    !ENDIF
  

ENDIF

! I.4 - BACK-SUBSTITUTION LAYER BY LAYER.
!-------------------------------------------------------------------------------

!cdir unroll=8
DO JLEV=KLEV,KTDIA,-1
	
		!IF ( LDMASKS(JLON) ) THEN

      IF (YDPHY%LRNUMX) THEN
        PFDN(JLON,JLEV)=PFDN(JLON,JLEV)+ZTU8(JLON,JLEV)&
         & *PFMC(JLON,JLEV)+ZTU9(JLON,JLEV)*PFMN(JLON,JLEV)
        PFDC(JLON,JLEV)=PFDC(JLON,JLEV)+ZTU6(JLON,JLEV)&
         & *PFMC(JLON,JLEV)+ZTU7(JLON,JLEV)*PFMN(JLON,JLEV)
        PFMN(JLON,JLEV-1)=PFMN(JLON,JLEV-1)+ZTU4(JLON,JLEV)&
         & *PFMC(JLON,JLEV)+ZTU5(JLON,JLEV)*PFMN(JLON,JLEV)
        PFMC(JLON,JLEV-1)=PFMC(JLON,JLEV-1)+ZTU2(JLON,JLEV)&
         & *PFMC(JLON,JLEV)+ZTU3(JLON,JLEV)*PFMN(JLON,JLEV)&
         & +ZTU1(JLON,JLEV)*PFMN(JLON,JLEV-1)
      ELSE
        PFDC(JLON,JLEV)=PFDC(JLON,JLEV)+ZTU6(JLON,JLEV)&
         & *PFMC(JLON,JLEV)
        PFMC(JLON,JLEV-1)=PFMC(JLON,JLEV-1)+ZTU2(JLON,JLEV)&
         & *PFMC(JLON,JLEV)
      ENDIF

    !ENDIF
  
ENDDO


END SUBROUTINE ACRANEB_SOLVS
