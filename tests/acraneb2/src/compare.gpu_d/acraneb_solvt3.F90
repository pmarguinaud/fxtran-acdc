SUBROUTINE ACRANEB_SOLVT3(YDPHY,KIDIA,KFDIA,KLON,KTDIA,KLEV,&
! - INPUT 1D
 & PEMIS,&
! - INPUT 2D
 & PNEB,PB1,PB2,PB3,PB4,&
! - INPUT/OUTPUT 2D
 & PA4C,PA5C,PA4N,PA5N,&
 & PF1DC,PF1MC,PF1DN,PF1MN,&
 & PF2DC,PF2MC,PF2DN,PF2MN,&
 & PF3DC,PF3MC,PF3DN,PF3MN, YDSTACK)
!$acc routine (ACRANEB_SOLVT3) seq

! Purpose:
! --------
!   ACRANEB_SOLVT - Adding system solver (thermal band, 3 RHS).

! Interface:
! ----------
! INPUT:
!   KIDIA - initial index for horizontal loops
!   KFDIA - final index for horizontal loops
!   KLON  - horizontal dimension of arrays
!   KTDIA - initial index for vertical loops (usually 1)
!   KLEV  - vertical dimension of full level arrays
!   PEMIS - surface emissivity
!   PNEB  - cloud fraction
!   PB1   - 1st intermediate storage for cloud geometry
!   PB2   - 2nd intermediate storage for cloud geometry
!   PB3   - 3rd intermediate storage for cloud geometry
!   PB4   - 4th intermediate storage for cloud geometry
! INPUT/OUTPUT:
!   PA4C  - clearsky diffuse transmissivity
!   PA5C  - clearsky diffuse reflectivity     
!   PA4N  - cloudy diffuse transmissivity
!   PA5N  - cloudy diffuse reflectivity        
!   PF1DC - 1st rhs term for clearsky downwards diffusive flux
!   PF1MC - 1st rhs term for clearsky upwards difffusive flux
!   PF1DN - 1st rhs term for cloudy downwards difffusive flux
!   PF1MN - 1st rhs term for cloudy upwards difffusive flux
!   PF2DC - 2nd rhs term for clearsky downwards diffusive flux
!   PF2MC - 2nd rhs term for clearsky upwards difffusive flux
!   PF2DN - 2nd rhs term for cloudy downwards difffusive flux
!   PF2MN - 2nd rhs term for cloudy upwards difffusive flux
!   PF3DC - 3rd rhs term for clearsky downwards diffusive flux
!   PF3MC - 3rd rhs term for clearsky upwards difffusive flux
!   PF3DN - 3rd rhs term for cloudy downwards difffusive flux
!   PF3MN - 3rd rhs term for cloudy upwards difffusive flux

! Externals:
! ----------

! Method:
! -------

! Reference:
! ----------

! Author:
! -------
!   1989-12, J.-F. Geleyn (original ACRANEB)

! Modifications:
! --------------
!   2009-10, T. Kral
!   Externalized from ACRANEB.
!
!   2013-11, J. Masek
!   Phasing to cy40t1.
! End Modifications
!-------------------------------------------------------------------------------

USE PARKIND1  ,ONLY : JPIM,    JPRB
USE YOMPHY    ,ONLY : TPHY
USE STACK_MOD
#include "stack.h"

IMPLICIT NONE

TYPE(TPHY)        ,INTENT(INOUT):: YDPHY
INTEGER(KIND=JPIM),INTENT(IN)  :: KLON 
INTEGER(KIND=JPIM),INTENT(IN)  :: KLEV 
INTEGER(KIND=JPIM),INTENT(IN)  :: KIDIA 
INTEGER(KIND=JPIM),INTENT(IN)  :: KFDIA 
INTEGER(KIND=JPIM),INTENT(IN)  :: KTDIA 

REAL(KIND=JPRB),INTENT(IN)     :: PEMIS(KLON)
REAL(KIND=JPRB),INTENT(IN)     :: PNEB(KLON,KLEV)
REAL(KIND=JPRB),INTENT(IN)     :: PB1(KLON,KLEV)
REAL(KIND=JPRB),INTENT(IN)     :: PB2(KLON,KLEV)
REAL(KIND=JPRB),INTENT(IN)     :: PB3(KLON,KLEV)
REAL(KIND=JPRB),INTENT(IN)     :: PB4(KLON,KLEV)
REAL(KIND=JPRB),INTENT(INOUT)  :: PA4C(KLON,KLEV)
REAL(KIND=JPRB),INTENT(INOUT)  :: PA5C(KLON,KLEV)
REAL(KIND=JPRB),INTENT(INOUT)  :: PA4N(KLON,KLEV)
REAL(KIND=JPRB),INTENT(INOUT)  :: PA5N(KLON,KLEV)
REAL(KIND=JPRB),INTENT(INOUT)  :: PF1DC(KLON,0:KLEV)
REAL(KIND=JPRB),INTENT(INOUT)  :: PF1MC(KLON,0:KLEV)
REAL(KIND=JPRB),INTENT(INOUT)  :: PF1DN(KLON,0:KLEV)
REAL(KIND=JPRB),INTENT(INOUT)  :: PF1MN(KLON,0:KLEV)
REAL(KIND=JPRB),INTENT(INOUT)  :: PF2DC(KLON,0:KLEV)
REAL(KIND=JPRB),INTENT(INOUT)  :: PF2MC(KLON,0:KLEV)
REAL(KIND=JPRB),INTENT(INOUT)  :: PF2DN(KLON,0:KLEV)
REAL(KIND=JPRB),INTENT(INOUT)  :: PF2MN(KLON,0:KLEV)
REAL(KIND=JPRB),INTENT(INOUT)  :: PF3DC(KLON,0:KLEV)
REAL(KIND=JPRB),INTENT(INOUT)  :: PF3MC(KLON,0:KLEV)
REAL(KIND=JPRB),INTENT(INOUT)  :: PF3DN(KLON,0:KLEV)
REAL(KIND=JPRB),INTENT(INOUT)  :: PF3MN(KLON,0:KLEV)
TYPE(STACK) :: YDSTACK
TYPE(STACK) :: YLSTACK

#include "abor1.intfb.h"

!-----------------------------------------------------------------------

!     LOCAL REAL SCALARS
REAL(KIND=JPRB) :: ZAL,ZTD1,ZTD2,ZTD3,ZTD4,ZTD5,ZTD6,ZTD7,ZTDS1,ZTDS2,&
                 & ZTDS3,ZTUS1

!     LOCAL REAL 1D ARRAYS
REAL(KIND=JPRB) :: ZAL1,ZAL2,ZBE1,ZBE2,ZDE1,&
                 & ZDE2,ZGA1,ZGA2

!     LOCAL REAL 2D ARRAYS
temp (REAL(KIND=JPRB), ZTU1, (KLON,KLEV))
temp (REAL(KIND=JPRB), ZTU2, (KLON,KLEV))
temp (REAL(KIND=JPRB), ZTU3, (KLON,KLEV))
temp (REAL(KIND=JPRB), ZTU4, (KLON,KLEV))
temp (REAL(KIND=JPRB), ZTU5, (KLON,KLEV))
temp (REAL(KIND=JPRB), ZTU6, (KLON,KLEV))
temp (REAL(KIND=JPRB), ZTU7, (KLON,KLEV))
temp (REAL(KIND=JPRB), ZTU8, (KLON,KLEV))
temp (REAL(KIND=JPRB), ZTU9, (KLON,KLEV))

!     LOCAL INTEGER SCALARS
INTEGER(KIND=JPIM) :: JLEV, JLON

YLSTACK = YDSTACK

alloc (ZTU1)
alloc (ZTU2)
alloc (ZTU3)
alloc (ZTU4)
alloc (ZTU5)
alloc (ZTU6)
alloc (ZTU7)
alloc (ZTU8)
alloc (ZTU9)



JLON = KIDIA


! - TEMPORAIRE(S) 1D

! ZAL1      : WEIGHT OF THE M OUTGOING C FLUX IN THE M INGOING C FLUX.
! ZAL2      : WEIGHT OF THE D OUTGOING C FLUX IN THE D INGOING C FLUX.
! ZBE1      : WEIGHT OF THE M OUTGOING C FLUX IN THE M INGOING N FLUX.
! ZBE2      : WEIGHT OF THE D OUTGOING C FLUX IN THE D INGOING N FLUX.
! ZGA1      : WEIGHT OF THE M OUTGOING N FLUX IN THE M INGOING C FLUX.
! ZGA2      : WEIGHT OF THE D OUTGOING N FLUX IN THE D INGOING C FLUX.
! ZDE1      : WEIGHT OF THE M OUTGOING N FLUX IN THE M INGOING N FLUX.
! ZDE2      : WEIGHT OF THE D OUTGOING N FLUX IN THE D INGOING N FLUX.

! - TEMPORAIRE(S) 2D (1:KLEV)

! ZTU1      : 1ST SUPERDIAGONAL COEFF. FOR THE ADDING SYSTEM.
! ZTU2      : 2ND SUPERDIAGONAL COEFF. FOR THE ADDING SYSTEM.
! ZTU3      : 3RD SUPERDIAGONAL COEFF. FOR THE ADDING SYSTEM.
! ZTU4      : 4TH SUPERDIAGONAL COEFF. FOR THE ADDING SYSTEM.
! ZTU5      : 5TH SUPERDIAGONAL COEFF. FOR THE ADDING SYSTEM.
! ZTU6      : 6TH SUPERDIAGONAL COEFF. FOR THE ADDING SYSTEM.
! ZTU7      : 7TH SUPERDIAGONAL COEFF. FOR THE ADDING SYSTEM.
! ZTU8      : 8TH SUPERDIAGONAL COEFF. FOR THE ADDING SYSTEM.
! ZTU9      : 9TH SUPERDIAGONAL COEFF. FOR THE ADDING SYSTEM.

! I.1 - FIRST LAYER, ELIMINATION (EASY).
!-------------------------------------------------------------------------------


  IF (YDPHY%LRNUMX) THEN
    ZAL1=PB2(JLON,KTDIA)
    ZAL2=PB1(JLON,KTDIA)
    ZBE1=1._JPRB-PB2(JLON,KTDIA)
    ZBE2=1._JPRB-PB1(JLON,KTDIA)
    ZGA1=1._JPRB-PB4(JLON,KTDIA)
    ZGA2=1._JPRB-PB3(JLON,KTDIA)
    ZDE1=PB4(JLON,KTDIA)
    ZDE2=PB3(JLON,KTDIA)
  ELSE
    PA4C(JLON,KTDIA)=PA4C(JLON,KTDIA)*PB1(JLON,KTDIA)&
     & +PA4N(JLON,KTDIA)*PNEB(JLON,KTDIA)
    PA5C(JLON,KTDIA)=PA5C(JLON,KTDIA)*PB1(JLON,KTDIA)&
     & +PA5N(JLON,KTDIA)*PNEB(JLON,KTDIA)
  ENDIF




  IF (YDPHY%LRNUMX) THEN
    PF1MC(JLON,KTDIA-1)=PA5C(JLON,KTDIA)*(ZAL2&
     & *PF1DC(JLON,KTDIA-1))+PF1MC(JLON,KTDIA-1)
    PF2MC(JLON,KTDIA-1)=PA5C(JLON,KTDIA)*(ZAL2&
     & *PF2DC(JLON,KTDIA-1))+PF2MC(JLON,KTDIA-1)
    PF3MC(JLON,KTDIA-1)=PA5C(JLON,KTDIA)*(ZAL2&
     & *PF3DC(JLON,KTDIA-1))+PF3MC(JLON,KTDIA-1)
    PF1DC(JLON,KTDIA)=PA4C(JLON,KTDIA)*(ZAL2&
     & *PF1DC(JLON,KTDIA-1))+PF1DC(JLON,KTDIA)
    PF2DC(JLON,KTDIA)=PA4C(JLON,KTDIA)*(ZAL2&
     & *PF2DC(JLON,KTDIA-1))+PF2DC(JLON,KTDIA)
    PF3DC(JLON,KTDIA)=PA4C(JLON,KTDIA)*(ZAL2&
     & *PF3DC(JLON,KTDIA-1))+PF3DC(JLON,KTDIA)
    PF1MN(JLON,KTDIA-1)=PA5N(JLON,KTDIA)*(ZBE2&
     & *PF1DC(JLON,KTDIA-1))+PF1MN(JLON,KTDIA-1)
    PF2MN(JLON,KTDIA-1)=PA5N(JLON,KTDIA)*(ZBE2&
     & *PF2DC(JLON,KTDIA-1))+PF2MN(JLON,KTDIA-1)
    PF3MN(JLON,KTDIA-1)=PA5N(JLON,KTDIA)*(ZBE2&
     & *PF3DC(JLON,KTDIA-1))+PF3MN(JLON,KTDIA-1)
    PF1DN(JLON,KTDIA)=PA4N(JLON,KTDIA)*(ZBE2&
     & *PF1DC(JLON,KTDIA-1))+PF1DN(JLON,KTDIA)
    PF2DN(JLON,KTDIA)=PA4N(JLON,KTDIA)*(ZBE2&
     & *PF2DC(JLON,KTDIA-1))+PF2DN(JLON,KTDIA)
    PF3DN(JLON,KTDIA)=PA4N(JLON,KTDIA)*(ZBE2&
     & *PF3DC(JLON,KTDIA-1))+PF3DN(JLON,KTDIA)
    ZTU1(JLON,KTDIA)=0._JPRB
    ZTU2(JLON,KTDIA)=ZAL1*PA4C(JLON,KTDIA)
    ZTU3(JLON,KTDIA)=ZGA1*PA4C(JLON,KTDIA)
    ZTU4(JLON,KTDIA)=ZBE1*PA4N(JLON,KTDIA)
    ZTU5(JLON,KTDIA)=ZDE1*PA4N(JLON,KTDIA)
    ZTU6(JLON,KTDIA)=ZAL1*PA5C(JLON,KTDIA)
    ZTU7(JLON,KTDIA)=ZGA1*PA5C(JLON,KTDIA)
    ZTU8(JLON,KTDIA)=ZBE1*PA5N(JLON,KTDIA)
    ZTU9(JLON,KTDIA)=ZDE1*PA5N(JLON,KTDIA)
  ELSE
    PF1MC(JLON,KTDIA-1)=PA5C(JLON,KTDIA)*PF1DC(JLON,KTDIA-1)&
     & +PF1MC(JLON,KTDIA-1)
    PF2MC(JLON,KTDIA-1)=PA5C(JLON,KTDIA)*PF2DC(JLON,KTDIA-1)&
     & +PF2MC(JLON,KTDIA-1)
    PF3MC(JLON,KTDIA-1)=PA5C(JLON,KTDIA)*PF3DC(JLON,KTDIA-1)&
     & +PF3MC(JLON,KTDIA-1)
    PF1DC(JLON,KTDIA)=PA4C(JLON,KTDIA)*PF1DC(JLON,KTDIA-1)&
     & +PF1DC(JLON,KTDIA)
    PF2DC(JLON,KTDIA)=PA4C(JLON,KTDIA)*PF2DC(JLON,KTDIA-1)&
     & +PF2DC(JLON,KTDIA)
    PF3DC(JLON,KTDIA)=PA4C(JLON,KTDIA)*PF3DC(JLON,KTDIA-1)&
     & +PF3DC(JLON,KTDIA)
    ZTU2(JLON,KTDIA)=PA4C(JLON,KTDIA)
    ZTU6(JLON,KTDIA)=PA5C(JLON,KTDIA)
  ENDIF ! LRNUMX



! I.2 - LOOP OVER THE LAYERS, PRELIMINARY COMPUTATIONS AND THEN ELIMINATION.
!-------------------------------------------------------------------------------

DO JLEV=KTDIA+1,KLEV

  
    IF (YDPHY%LRNUMX) THEN
      ZAL1=PB2(JLON,JLEV)
      ZAL2=PB1(JLON,JLEV)
      ZBE1=1._JPRB-PB2(JLON,JLEV)
      ZBE2=1._JPRB-PB1(JLON,JLEV)
      ZGA1=1._JPRB-PB4(JLON,JLEV)
      ZGA2=1._JPRB-PB3(JLON,JLEV)
      ZDE1=PB4(JLON,JLEV)
      ZDE2=PB3(JLON,JLEV)
    ELSE
      PA4C(JLON,JLEV)=PA4C(JLON,JLEV)*PB1(JLON,JLEV)&
       & +PA4N(JLON,JLEV)*PNEB(JLON,JLEV)
      PA5C(JLON,JLEV)=PA5C(JLON,JLEV)*PB1(JLON,JLEV)&
       & +PA5N(JLON,JLEV)*PNEB(JLON,JLEV)
    ENDIF
  

  

    IF (YDPHY%LRNUMX) THEN
      ZTD1=1._JPRB/(1._JPRB-PA5C(JLON,JLEV)*(ZAL2&
       & *ZTU6(JLON,JLEV-1)+ZGA2*ZTU8(JLON,JLEV-1)))
      PF1MC(JLON,JLEV-1)=(ZTD1*PA5C(JLON,JLEV))*(ZAL2&
       & *PF1DC(JLON,JLEV-1)+ZGA2*PF1DN(JLON,JLEV-1))&
       & +ZTD1*PF1MC(JLON,JLEV-1)
      PF2MC(JLON,JLEV-1)=(ZTD1*PA5C(JLON,JLEV))*(ZAL2&
       & *PF2DC(JLON,JLEV-1)+ZGA2*PF2DN(JLON,JLEV-1))&
       & +ZTD1*PF2MC(JLON,JLEV-1)
      PF3MC(JLON,JLEV-1)=(ZTD1*PA5C(JLON,JLEV))*(ZAL2&
       & *PF3DC(JLON,JLEV-1)+ZGA2*PF3DN(JLON,JLEV-1))&
       & +ZTD1*PF3MC(JLON,JLEV-1)
      ZTU1(JLON,JLEV)=(ZTD1*PA5C(JLON,JLEV))*(ZAL2&
       & *ZTU7(JLON,JLEV-1)+ZGA2*ZTU9(JLON,JLEV-1))
      ZTU2(JLON,JLEV)=(ZTD1*PA4C(JLON,JLEV))*ZAL1
      ZTU3(JLON,JLEV)=(ZTD1*PA4C(JLON,JLEV))*ZGA1
      ZTD2=PA5N(JLON,JLEV)*(ZBE2*ZTU6(JLON,JLEV-1)&
       & +ZDE2*ZTU8(JLON,JLEV-1))
      ZTD3=1._JPRB/(1._JPRB-PA5N(JLON,JLEV)*(ZBE2&
       & *ZTU7(JLON,JLEV-1)+ZDE2*ZTU9(JLON,JLEV-1))&
       & -ZTD2*ZTU1(JLON,JLEV))
      PF1MN(JLON,JLEV-1)=ZTD3*(PA5N(JLON,JLEV)*(ZBE2&
       & *PF1DC(JLON,JLEV-1)+ZDE2*PF1DN(JLON,JLEV-1))&
       & +ZTD2*PF1MC(JLON,JLEV-1))+ZTD3*PF1MN(JLON,JLEV-1)
      PF2MN(JLON,JLEV-1)=ZTD3*(PA5N(JLON,JLEV)*(ZBE2&
       & *PF2DC(JLON,JLEV-1)+ZDE2*PF2DN(JLON,JLEV-1))&
       & +ZTD2*PF2MC(JLON,JLEV-1))+ZTD3*PF2MN(JLON,JLEV-1)
      PF3MN(JLON,JLEV-1)=ZTD3*(PA5N(JLON,JLEV)*(ZBE2&
       & *PF3DC(JLON,JLEV-1)+ZDE2*PF3DN(JLON,JLEV-1))&
       & +ZTD2*PF3MC(JLON,JLEV-1))+ZTD3*PF3MN(JLON,JLEV-1)
      ZTU4(JLON,JLEV)=ZTD3*(PA4N(JLON,JLEV)&
       & *ZBE1+ZTD2*ZTU2(JLON,JLEV))
      ZTU5(JLON,JLEV)=ZTD3*(PA4N(JLON,JLEV)&
       & *ZDE1+ZTD2*ZTU3(JLON,JLEV))
      ZTD4=PA4C(JLON,JLEV)*(ZAL2*ZTU6(JLON,JLEV-1)&
       & +ZGA2*ZTU8(JLON,JLEV-1))
      ZTD5=PA4C(JLON,JLEV)*(ZAL2*ZTU7(JLON,JLEV-1)&
       & +ZGA2*ZTU9(JLON,JLEV-1))
      PF1DC(JLON,JLEV)=PA4C(JLON,JLEV)*(ZAL2*PF1DC(JLON,JLEV-1)&
       & +ZGA2*PF1DN(JLON,JLEV-1))+ZTD4*PF1MC(JLON,JLEV-1)&
       & +ZTD5*PF1MN(JLON,JLEV-1)+PF1DC(JLON,JLEV)
      PF2DC(JLON,JLEV)=PA4C(JLON,JLEV)*(ZAL2*PF2DC(JLON,JLEV-1)&
       & +ZGA2*PF2DN(JLON,JLEV-1))+ZTD4*PF2MC(JLON,JLEV-1)&
       & +ZTD5*PF2MN(JLON,JLEV-1)+PF2DC(JLON,JLEV)
      PF3DC(JLON,JLEV)=PA4C(JLON,JLEV)*(ZAL2*PF3DC(JLON,JLEV-1)&
       & +ZGA2*PF3DN(JLON,JLEV-1))+ZTD4*PF3MC(JLON,JLEV-1)&
       & +ZTD5*PF3MN(JLON,JLEV-1)+PF3DC(JLON,JLEV)
      ZTU6(JLON,JLEV)=PA5C(JLON,JLEV)*ZAL1+ZTD4&
       & *ZTU2(JLON,JLEV)+ZTD5*ZTU4(JLON,JLEV)
      ZTU7(JLON,JLEV)=PA5C(JLON,JLEV)*ZGA1+ZTD4&
       & *ZTU3(JLON,JLEV)+ZTD5*ZTU5(JLON,JLEV)
      ZTD6=PA4N(JLON,JLEV)*(ZBE2*ZTU6(JLON,JLEV-1)&
       & +ZDE2*ZTU8(JLON,JLEV-1))
      ZTD7=PA4N(JLON,JLEV)*(ZBE2*ZTU7(JLON,JLEV-1)&
       & +ZDE2*ZTU9(JLON,JLEV-1))
      PF1DN(JLON,JLEV)=PA4N(JLON,JLEV)*(ZBE2*PF1DC(JLON,JLEV-1)&
       & +ZDE2*PF1DN(JLON,JLEV-1))+ZTD6*PF1MC(JLON,JLEV-1)&
       & +ZTD7*PF1MN(JLON,JLEV-1)+PF1DN(JLON,JLEV)
      PF2DN(JLON,JLEV)=PA4N(JLON,JLEV)*(ZBE2*PF2DC(JLON,JLEV-1)&
       & +ZDE2*PF2DN(JLON,JLEV-1))+ZTD6*PF2MC(JLON,JLEV-1)&
       & +ZTD7*PF2MN(JLON,JLEV-1)+PF2DN(JLON,JLEV)
      PF3DN(JLON,JLEV)=PA4N(JLON,JLEV)*(ZBE2*PF3DC(JLON,JLEV-1)&
       & +ZDE2*PF3DN(JLON,JLEV-1))+ZTD6*PF3MC(JLON,JLEV-1)&
       & +ZTD7*PF3MN(JLON,JLEV-1)+PF3DN(JLON,JLEV)
      ZTU8(JLON,JLEV)=PA5N(JLON,JLEV)*ZBE1+ZTD6&
       & *ZTU2(JLON,JLEV)+ZTD7*ZTU4(JLON,JLEV)
      ZTU9(JLON,JLEV)=PA5N(JLON,JLEV)*ZDE1+ZTD6&
       & *ZTU3(JLON,JLEV)+ZTD7*ZTU5(JLON,JLEV)
    ELSE
      ZTD1=1._JPRB/(1._JPRB-PA5C(JLON,JLEV)*ZTU6(JLON,JLEV-1))
      PF1MC(JLON,JLEV-1)=ZTD1*PA5C(JLON,JLEV)&
       & *PF1DC(JLON,JLEV-1)+ZTD1*PF1MC(JLON,JLEV-1)
      PF2MC(JLON,JLEV-1)=ZTD1*PA5C(JLON,JLEV)&
       & *PF2DC(JLON,JLEV-1)+ZTD1*PF2MC(JLON,JLEV-1)
      PF3MC(JLON,JLEV-1)=ZTD1*PA5C(JLON,JLEV)&
       & *PF3DC(JLON,JLEV-1)+ZTD1*PF3MC(JLON,JLEV-1)
      ZTU2(JLON,JLEV)=ZTD1*PA4C(JLON,JLEV)
      ZTD4=PA4C(JLON,JLEV)*ZTU6(JLON,JLEV-1)
      PF1DC(JLON,JLEV)=PA4C(JLON,JLEV)*PF1DC(JLON,JLEV-1)&
       & +ZTD4*PF1MC(JLON,JLEV-1)+PF1DC(JLON,JLEV)
      PF2DC(JLON,JLEV)=PA4C(JLON,JLEV)*PF2DC(JLON,JLEV-1)&
       & +ZTD4*PF2MC(JLON,JLEV-1)+PF2DC(JLON,JLEV)
      PF3DC(JLON,JLEV)=PA4C(JLON,JLEV)*PF3DC(JLON,JLEV-1)&
       & +ZTD4*PF3MC(JLON,JLEV-1)+PF3DC(JLON,JLEV)
      ZTU6(JLON,JLEV)=PA5C(JLON,JLEV)+ZTD4*ZTU2(JLON,JLEV)
    ENDIF ! LRNUMX

  
ENDDO

! I.3 - SURFACE TREATMENT, ELIMINATION AND BACK-SUBSTITUTION.
!-------------------------------------------------------------------------------

! TODO: INCLUDE EMISIVITY IN ZAC(N)3 & ZAC(N)5 ARRAYS, THIS REQUIRES TO 
!       INCREASE DIMENSION OF ZAC(N) TO 0:KLEV



  ZAL=1._JPRB-PEMIS(JLON)

  IF (YDPHY%LRNUMX) THEN
    ZTDS1=1._JPRB/(1._JPRB-ZAL*ZTU6(JLON,KLEV))
    PF1MC(JLON,KLEV)=ZTDS1*(ZAL*PF1DC(JLON,KLEV)+PF1MC(JLON,KLEV))
    PF2MC(JLON,KLEV)=ZTDS1*(ZAL*PF2DC(JLON,KLEV)+PF2MC(JLON,KLEV))
    PF3MC(JLON,KLEV)=ZTDS1*(ZAL*PF3DC(JLON,KLEV)+PF3MC(JLON,KLEV))
    ZTUS1=(ZTDS1*ZAL)*ZTU7(JLON,KLEV)
    ZTDS2=ZAL*ZTU8(JLON,KLEV)
    ZTDS3=1._JPRB/(1._JPRB-ZAL*ZTU9(JLON,KLEV)-ZTDS2*ZTUS1)
    PF1MN(JLON,KLEV)=ZTDS3*(ZAL*PF1DN(JLON,KLEV)&
     & +ZTDS2*PF1MC(JLON,KLEV)+PF1MN(JLON,KLEV))
    PF2MN(JLON,KLEV)=ZTDS3*(ZAL*PF2DN(JLON,KLEV)&
     & +ZTDS2*PF2MC(JLON,KLEV)+PF2MN(JLON,KLEV))
    PF3MN(JLON,KLEV)=ZTDS3*(ZAL*PF3DN(JLON,KLEV)&
     & +ZTDS2*PF3MC(JLON,KLEV)+PF3MN(JLON,KLEV))
    PF1MC(JLON,KLEV)=PF1MC(JLON,KLEV)+ZTUS1*PF1MN(JLON,KLEV)
    PF2MC(JLON,KLEV)=PF2MC(JLON,KLEV)+ZTUS1*PF2MN(JLON,KLEV)
    PF3MC(JLON,KLEV)=PF3MC(JLON,KLEV)+ZTUS1*PF3MN(JLON,KLEV)
  ELSE
    ZTDS1=1._JPRB/(1._JPRB-ZAL*ZTU6(JLON,KLEV))
    PF1MC(JLON,KLEV)=ZTDS1*(ZAL*PF1DC(JLON,KLEV)+PF1MC(JLON,KLEV))
    PF2MC(JLON,KLEV)=ZTDS1*(ZAL*PF2DC(JLON,KLEV)+PF2MC(JLON,KLEV))
    PF3MC(JLON,KLEV)=ZTDS1*(ZAL*PF3DC(JLON,KLEV)+PF3MC(JLON,KLEV))
  ENDIF



! I.4 - BACK-SUBSTITUTION LAYER BY LAYER.
!-------------------------------------------------------------------------------

!cdir unroll=8
DO JLEV=KLEV,KTDIA,-1
  

    IF (YDPHY%LRNUMX) THEN
      PF1DN(JLON,JLEV)=PF1DN(JLON,JLEV)+ZTU8(JLON,JLEV)&
       & *PF1MC(JLON,JLEV)+ZTU9(JLON,JLEV)*PF1MN(JLON,JLEV)
      PF2DN(JLON,JLEV)=PF2DN(JLON,JLEV)+ZTU8(JLON,JLEV)&
       & *PF2MC(JLON,JLEV)+ZTU9(JLON,JLEV)*PF2MN(JLON,JLEV)
      PF3DN(JLON,JLEV)=PF3DN(JLON,JLEV)+ZTU8(JLON,JLEV)&
       & *PF3MC(JLON,JLEV)+ZTU9(JLON,JLEV)*PF3MN(JLON,JLEV)
      PF1DC(JLON,JLEV)=PF1DC(JLON,JLEV)+ZTU6(JLON,JLEV)&
       & *PF1MC(JLON,JLEV)+ZTU7(JLON,JLEV)*PF1MN(JLON,JLEV)
      PF2DC(JLON,JLEV)=PF2DC(JLON,JLEV)+ZTU6(JLON,JLEV)&
       & *PF2MC(JLON,JLEV)+ZTU7(JLON,JLEV)*PF2MN(JLON,JLEV)
      PF3DC(JLON,JLEV)=PF3DC(JLON,JLEV)+ZTU6(JLON,JLEV)&
       & *PF3MC(JLON,JLEV)+ZTU7(JLON,JLEV) *PF3MN(JLON,JLEV)
      PF1MN(JLON,JLEV-1)=PF1MN(JLON,JLEV-1)+ZTU4(JLON,JLEV)&
       & *PF1MC(JLON,JLEV)+ZTU5(JLON,JLEV)*PF1MN(JLON,JLEV)
      PF2MN(JLON,JLEV-1)=PF2MN(JLON,JLEV-1)+ZTU4(JLON,JLEV)&
       & *PF2MC(JLON,JLEV)+ZTU5(JLON,JLEV)*PF2MN(JLON,JLEV)
      PF3MN(JLON,JLEV-1)=PF3MN(JLON,JLEV-1)+ZTU4(JLON,JLEV)&
       & *PF3MC(JLON,JLEV)+ZTU5(JLON,JLEV)*PF3MN(JLON,JLEV)
      PF1MC(JLON,JLEV-1)=PF1MC(JLON,JLEV-1)+ZTU2(JLON,JLEV)&
       & *PF1MC(JLON,JLEV)+ZTU3(JLON,JLEV)*PF1MN(JLON,JLEV)&
       & +ZTU1(JLON,JLEV)*PF1MN(JLON,JLEV-1)
      PF2MC(JLON,JLEV-1)=PF2MC(JLON,JLEV-1)+ZTU2(JLON,JLEV)&
       & *PF2MC(JLON,JLEV)+ZTU3(JLON,JLEV)*PF2MN(JLON,JLEV)&
       & +ZTU1(JLON,JLEV)*PF2MN(JLON,JLEV-1)
      PF3MC(JLON,JLEV-1)=PF3MC(JLON,JLEV-1)+ZTU2(JLON,JLEV)&
       & *PF3MC(JLON,JLEV)+ZTU3(JLON,JLEV)*PF3MN(JLON,JLEV)&
       & +ZTU1(JLON,JLEV)*PF3MN(JLON,JLEV-1)
    ELSE
      PF1DC(JLON,JLEV)=PF1DC(JLON,JLEV)+ZTU6(JLON,JLEV)&
       & *PF1MC(JLON,JLEV)
      PF2DC(JLON,JLEV)=PF2DC(JLON,JLEV)+ZTU6(JLON,JLEV)&
       & *PF2MC(JLON,JLEV)
      PF3DC(JLON,JLEV)=PF3DC(JLON,JLEV)+ZTU6(JLON,JLEV)&
       & *PF3MC(JLON,JLEV)
      PF1MC(JLON,JLEV-1)=PF1MC(JLON,JLEV-1)+ZTU2(JLON,JLEV)&
       & *PF1MC(JLON,JLEV)
      PF2MC(JLON,JLEV-1)=PF2MC(JLON,JLEV-1)+ZTU2(JLON,JLEV)&
       & *PF2MC(JLON,JLEV)
      PF3MC(JLON,JLEV-1)=PF3MC(JLON,JLEV-1)+ZTU2(JLON,JLEV)&
       & *PF3MC(JLON,JLEV)
    ENDIF ! LRNUMX

  
ENDDO


END SUBROUTINE ACRANEB_SOLVT3
