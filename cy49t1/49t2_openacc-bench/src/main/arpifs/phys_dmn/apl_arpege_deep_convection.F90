SUBROUTINE APL_ARPEGE_DEEP_CONVECTION (YDMF_PHYS_BASE_STATE, YDGEOMETRY, YDCPG_BNDS, YDCPG_OPTS,    &
& YDMF_PHYS, YDCPG_DYN0, YDMF_PHYS_SURF, YDVARS, YDMODEL, PFPCOR, PQV, PTENHA, PTENQVA, PTENT, PRDG_CVGQ, PRDG_CVGT)

!**** *APL_ARPEGE_DEEP_CONVECTION*

!     Author. 
!     ------- 
!      Philippe Marguinaud *METEO-FRANCE*
!      Original : 28-04-2021

USE YOMHOOK, ONLY : LHOOK, DR_HOOK, JPHOOK
USE PARKIND1, ONLY : JPIM, JPRB

USE MF_PHYS_BASE_STATE_TYPE_MOD &
                       , ONLY : MF_PHYS_BASE_STATE_TYPE
USE GEOMETRY_MOD       , ONLY : GEOMETRY
USE CPG_OPTS_TYPE_MOD  , ONLY : CPG_BNDS_TYPE, CPG_OPTS_TYPE
USE MF_PHYS_TYPE_MOD   , ONLY : MF_PHYS_TYPE
USE CPG_TYPE_MOD       , ONLY : CPG_DYN_TYPE
USE MF_PHYS_SURFACE_TYPE_MOD  &
                       , ONLY : MF_PHYS_SURF_TYPE
USE FIELD_VARIABLES_MOD, ONLY : FIELD_VARIABLES
USE YOMCFU             , ONLY : TCFU
USE TYPE_MODEL         , ONLY : MODEL


IMPLICIT NONE

TYPE(MF_PHYS_BASE_STATE_TYPE),  INTENT(IN)    :: YDMF_PHYS_BASE_STATE
TYPE(GEOMETRY),                 INTENT(IN)    :: YDGEOMETRY
TYPE(CPG_BNDS_TYPE),            INTENT(IN)    :: YDCPG_BNDS
TYPE(CPG_OPTS_TYPE),            INTENT(IN)    :: YDCPG_OPTS
TYPE(MF_PHYS_TYPE),             INTENT(INOUT) :: YDMF_PHYS
TYPE(CPG_DYN_TYPE),             INTENT(IN)    :: YDCPG_DYN0
TYPE(MF_PHYS_SURF_TYPE),        INTENT(INOUT) :: YDMF_PHYS_SURF
TYPE(FIELD_VARIABLES),          INTENT(INOUT) :: YDVARS
TYPE(MODEL),                    INTENT(IN)    :: YDMODEL
REAL(KIND=JPRB),                INTENT(OUT)   :: PFPCOR(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB),                INTENT(IN)    :: PQV(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB),                INTENT(OUT)   :: PTENHA(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB),                INTENT(OUT)   :: PTENQVA(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB),                INTENT(OUT)   :: PTENT(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB),                INTENT(IN)    :: PRDG_CVGQ(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB),                INTENT(IN)    :: PRDG_CVGT(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)

#include "acuptq.intfb.h"
#include "cucalln_mf.intfb.h"
#include "culight.intfb.h"

INTEGER(KIND=JPIM) :: IBASC(YDCPG_OPTS%KLON)
INTEGER(KIND=JPIM) :: IBOTSC(YDCPG_OPTS%KLON)
INTEGER(KIND=JPIM) :: ICBOT(YDCPG_OPTS%KLON)
INTEGER(KIND=JPIM) :: ICBOT_LIG(YDCPG_OPTS%KLON)
INTEGER(KIND=JPIM) :: ICTOP(YDCPG_OPTS%KLON)
INTEGER(KIND=JPIM) :: ICTOP_LIG(YDCPG_OPTS%KLON)
INTEGER(KIND=JPIM) :: ISPPN2D
INTEGER(KIND=JPIM) :: ITOPC(YDCPG_OPTS%KLON)
INTEGER(KIND=JPIM) :: ITYPE(YDCPG_OPTS%KLON)
LOGICAL :: LLCUM(YDCPG_OPTS%KLON)
LOGICAL :: LLCUM_LIG(YDCPG_OPTS%KLON)
LOGICAL :: LLDSLPHY
LOGICAL :: LLLAND(YDCPG_OPTS%KLON)
LOGICAL :: LLLINOX(YDCPG_OPTS%KLON)
LOGICAL :: LLSC(YDCPG_OPTS%KLON)
LOGICAL :: LLSHCV(YDCPG_OPTS%KLON)
REAL(KIND=JPRB) :: ZACPR(YDCPG_OPTS%KLON)
REAL(KIND=JPRB) :: ZCDEPTH(YDCPG_OPTS%KLON)
REAL(KIND=JPRB) :: ZCEN(1,1,1)
REAL(KIND=JPRB) :: ZCTOPH(YDCPG_OPTS%KLON)
REAL(KIND=JPRB) :: ZCUCONVCA(YDCPG_OPTS%KLON)
REAL(KIND=JPRB) :: ZDIFF(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZDXTDK(YDCPG_OPTS%KLON)
REAL(KIND=JPRB) :: ZFCQLF(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZFCQLI(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZFHPCL(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZFHPCN(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZGAW(YDCPG_OPTS%KLON)
REAL(KIND=JPRB) :: ZGEOM1(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZGEOMH(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZGP2DSPPA(YDCPG_OPTS%KLON,1)
REAL(KIND=JPRB) :: ZICE(YDCPG_OPTS%KLON)
REAL(KIND=JPRB) :: ZLCRIT_AER(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZLIGH_CTG(YDCPG_OPTS%KLON)
REAL(KIND=JPRB) :: ZLISUM(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZLRAIN(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZLU(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZLUDE(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZLUDELI(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG,4)
REAL(KIND=JPRB) :: ZMFD(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZMFDDE_RATE(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZMFU(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZMFUDE_RATE(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZPRECMX(YDCPG_OPTS%KLON)
REAL(KIND=JPRB) :: ZRSUD(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG,2)
REAL(KIND=JPRB) :: ZSCAV(1)
REAL(KIND=JPRB) :: ZSNDE(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG,2)
REAL(KIND=JPRB) :: ZTENC(1,1,1)
REAL(KIND=JPRB) :: ZTENT(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZTENQ(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZTENU(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZTENV(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZTENTA(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZTENQA(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZVDIFTS
REAL(KIND=JPRB) :: ZVERVEL(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZWMEAN(YDCPG_OPTS%KLON)
REAL(KIND=JPRB) :: ZVDISCU(YDCPG_OPTS%KLON)
REAL(KIND=JPRB) :: ZWMFU(YDCPG_OPTS%KLON)
REAL(KIND=JPRB) :: ZWU(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZLGLAC(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZTENDT(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZTENDQ(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)

! argument that have to be passed to culight but usell in arpege context so set to 0
REAL(KIND=JPRB) :: ZQPFROZ(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZCHARGE(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)

INTEGER (KIND=JPIM) :: JLEV
INTEGER (KIND=JPIM) :: JLON
LOGICAL :: LLPTQ


REAL (KIND=JPHOOK) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK ('APL_ARPEGE_DEEP_CONVECTION', 0, ZHOOK_HANDLE)

ASSOCIATE (KLON => YDCPG_OPTS%KLON, KIDIA => YDCPG_BNDS%KIDIA, KFDIA => YDCPG_BNDS%KFDIA, KFLEVG => YDCPG_OPTS%KFLEVG)

! DEEP CONVECTION SCHEME : IFS deep convection scheme

!$ACDC PARALLEL, TARGET=OpenMP/OpenMPSingleColumn/OpenACCSingleColumn,NAME=INIT {

DO JLEV=1,KFLEVG
  DO JLON = KIDIA, KFDIA
    ZGEOM1(JLON,JLEV)     = YDMF_PHYS_BASE_STATE%YCPG_DYN%PHIF(JLON,JLEV)-YDMF_PHYS_BASE_STATE%YCPG_DYN%PHI(JLON,KFLEVG)
    ZVERVEL(JLON,JLEV)    = YDCPG_DYN0%CTY%VVEL(JLON,JLEV)*YDMF_PHYS_BASE_STATE%YCPG_PHY%PREHYDF(JLON,JLEV)
    ZLISUM(JLON,JLEV)     = 0.0_JPRB
    ZLCRIT_AER(JLON,JLEV) = 5.E-4_JPRB
  ENDDO
ENDDO

DO JLEV=0,KFLEVG
  DO JLON = KIDIA, KFDIA
    ZGEOMH(JLON,JLEV)=YDMF_PHYS_BASE_STATE%YCPG_DYN%PHI(JLON,JLEV)-YDMF_PHYS_BASE_STATE%YCPG_DYN%PHI(JLON,KFLEVG)
    ZFCQLF(JLON,JLEV)=0.0_JPRB
    ZFCQLI(JLON,JLEV)=0.0_JPRB
  ENDDO
ENDDO

DO JLON = KIDIA, KFDIA
  LLLAND(JLON)    = (YDMF_PHYS_SURF%GSD_VF%PLSM(JLON)>0.5_JPRB)
  LLSHCV(JLON)    = .FALSE.
  ZCUCONVCA(JLON) = 0.0_JPRB
  ZACPR(JLON)     = 0.0_JPRB
  ZDXTDK(JLON)    = YDGEOMETRY%YRGEM%RDELXN/YDVARS%GEOMETRY%GM%T0(JLON)
ENDDO

!$ACDC }

LLPTQ = .FALSE.

!$ACDC PARALLEL, TARGET=OpenMP/OpenMPSingleColumn/OpenACCSingleColumn,NAME=ACUPTQ {

CALL ACUPTQ (YDMODEL%YRCST,  KLON, KIDIA, KFDIA, KFLEVG, 0, LLPTQ, YDMF_PHYS%OUT%FRSO,                        &
& YDMF_PHYS%OUT%FRTH, YDMF_PHYS%OUT%DIFCQ, YDMF_PHYS%OUT%DIFCS, YDMF_PHYS%OUT%DIFTQ, YDMF_PHYS%OUT%DIFTS,     &
& YDMF_PHYS%OUT%FCCQL, YDMF_PHYS%OUT%FCCQN, YDMF_PHYS%OUT%FPLCL, YDMF_PHYS%OUT%FPLCN, YDMF_PHYS%OUT%FPEVPCL,  &
& YDMF_PHYS%OUT%FPEVPCN, YDMF_PHYS%OUT%FPFPCL, YDMF_PHYS%OUT%FPFPCN, YDMF_PHYS_BASE_STATE%YCPG_PHY%XYB%RDELP, &
& YDMF_PHYS_BASE_STATE%T, YDMF_PHYS_BASE_STATE%Q, YDMF_PHYS_BASE_STATE%YGSP_RR%T, PTENHA, PTENQVA)

!$ACDC }
    

!$ACDC PARALLEL, TARGET=OpenMP/OpenMPSingleColumn/OpenACCSingleColumn,NAME=ZERO {

DO JLEV=1,KFLEVG
  DO JLON = KIDIA, KFDIA 
    ZTENT(JLON,JLEV) = PTENHA(JLON,JLEV)/YDMF_PHYS_BASE_STATE%YCPG_DYN%RCP%CP(JLON,JLEV)
    ZTENQ(JLON,JLEV) = PTENQVA(JLON,JLEV)
    ZTENU(JLON,JLEV) = 0.0_JPRB
    ZTENV(JLON,JLEV) = 0.0_JPRB
    ZTENDT(JLON,JLEV) = PRDG_CVGT(JLON,JLEV)
    ZTENDQ(JLON,JLEV) = PRDG_CVGQ(JLON,JLEV)    
  ENDDO
ENDDO

!$ACDC }

LLDSLPHY=.TRUE.
ZVDIFTS = 0._JPRB
ISPPN2D = 0

!$ACDC PARALLEL, TARGET=OpenMP/OpenMPSingleColumn/OpenACCSingleColumn,NAME=CUCALLN_MF {
         
CALL CUCALLN_MF (YDCPG_OPTS%RPLDARE, YDCPG_OPTS%RPLRG, YDCPG_OPTS%NSTEP, YDMODEL%YRML_PHY_EC%YRTHF, YDMODEL%YRCST,         &
& YDMODEL%YRML_PHY_RAD%YRERAD, YDMODEL%YRML_PHY_SLIN, YDMODEL%YRML_PHY_EC, YDMODEL%YRML_GCONF%YGFL,                        &
& YDMODEL%YRML_CHEM%YRCHEM, YDMODEL%YRML_GCONF%YRSPP_CONFIG, YDMODEL%YRML_PHY_STOCH%YRPERTPAR,                             &
& KIDIA, KFDIA, KLON, 0, KFLEVG, ZDXTDK, ISPPN2D,                                                                          &
& YDMODEL%YRML_PHY_MF%YRPHY%YRCAPE%LMCAPEA, LLLAND,                                                                        &
& LLDSLPHY, YDMODEL%YRML_PHY_MF%YRPHY2%TSPHY, ZVDIFTS, YDMF_PHYS_BASE_STATE%T, PQV, YDMF_PHYS_BASE_STATE%U,                &
& YDMF_PHYS_BASE_STATE%V, ZLISUM, ZVERVEL, YDMF_PHYS%OUT%DIFTQ, YDMF_PHYS%OUT%DIFTS, YDMF_PHYS_BASE_STATE%YCPG_PHY%PREHYD, &
& YDMF_PHYS_BASE_STATE%YCPG_PHY%PREHYDF, YDMF_PHYS_BASE_STATE%YCPG_PHY%PREHYD, ZGEOM1, ZGEOMH, YDVARS%GEOMETRY%GM%T0,      &
& ZCUCONVCA, ZGP2DSPPA, ZTENT, ZTENQ, ZTENU, ZTENV, ZTENDT, ZTENDQ, ZACPR, ITOPC, IBASC, ITYPE, ICBOT, ICTOP, IBOTSC,      &
& LLCUM, LLSC, ICBOT_LIG, ICTOP_LIG, LLCUM_LIG,                                                                            &
& LLSHCV, ZLCRIT_AER, ZLU, ZLUDE, ZLUDELI, ZSNDE, ZMFU, ZMFD, ZLGLAC, YDMF_PHYS%OUT%DIFCQ, YDMF_PHYS%OUT%DIFCS,            &
& ZFHPCL, ZFHPCN, YDMF_PHYS%OUT%FPLCL, YDMF_PHYS%OUT%FPLCN, ZLRAIN, ZRSUD, YDMF_PHYS%OUT%STRCU, YDMF_PHYS%OUT%STRCV,       &
& ZFCQLF, ZFCQLI, ZMFUDE_RATE, ZMFDDE_RATE, YDMF_PHYS%OUT%CAPE, ZWU, ZWMEAN, ZVDISCU, ZDIFF, 0, ZCEN, ZTENC, ZSCAV, ZSCAV)

!$ACDC }

!$ACDC PARALLEL, TARGET=OpenMP/OpenMPSingleColumn/OpenACCSingleColumn,NAME=COPY {

DO JLEV=0,KFLEVG
  DO JLON = KIDIA, KFDIA 
    PFPCOR  (JLON,JLEV)=YDMF_PHYS%OUT%FPLCL(JLON,JLEV)+YDMF_PHYS%OUT%FPLCN(JLON,JLEV)
    YDMF_PHYS%OUT%FCCQL  (JLON,JLEV)=YDMF_PHYS%OUT%FPLCL(JLON,JLEV)
    YDMF_PHYS%OUT%FCCQN  (JLON,JLEV)=YDMF_PHYS%OUT%FPLCN(JLON,JLEV)
    YDMF_PHYS%OUT%FPFPCL (JLON,JLEV)=YDMF_PHYS%OUT%FPLCL(JLON,JLEV)
    YDMF_PHYS%OUT%FPFPCN (JLON,JLEV)=YDMF_PHYS%OUT%FPLCN(JLON,JLEV)
    YDMF_PHYS%OUT%FPEVPCL(JLON,JLEV)=0._JPRB
    YDMF_PHYS%OUT%FPEVPCN(JLON,JLEV)=0._JPRB
  ENDDO
ENDDO

DO JLEV=1,KFLEVG
  DO JLON = KIDIA, KFDIA  
     YDMF_PHYS%OUT%FPEVPCL(JLON,JLEV)=YDMF_PHYS%OUT%FPEVPCL(JLON,JLEV-1) - ZSNDE(JLON,JLEV,1)
     YDMF_PHYS%OUT%FPEVPCN(JLON,JLEV)=YDMF_PHYS%OUT%FPEVPCN(JLON,JLEV-1) - ZSNDE(JLON,JLEV,2)
  ENDDO
ENDDO

!$ACDC }

  
IF(YDCPG_OPTS%LFLASH) THEN

! LIGHTNING PARAMETERIZATION. OUTPUT IS PFLASH, TOTAL LIGHTNING FLASH RATES.

!$ACDC PARALLEL, TARGET=OpenMP/OpenMPSingleColumn/OpenACCSingleColumn,NAME=CULIGHT {

  ZGAW(:)=YDVARS%GEOMETRY%GAW%P(:)
  ZQPFROZ(:,:)=0
  ZCHARGE(:,:)=0
  CALL CULIGHT (YDCPG_OPTS%RPLDARE, YDCPG_OPTS%RPLRG, YDMODEL%YRML_PHY_EC%YRTHF, YDMODEL%YRCST, YDMODEL%YRML_PHY_EC%YREPHY, &
  & YDMODEL%YRML_GCONF%YGFL, YDMODEL%YRML_PHY_EC%YRECUMF, KIDIA, KFDIA,                                                     &
  & KLON, KFLEVG, ZGAW, ZGAW, YDMF_PHYS_BASE_STATE%YCPG_PHY%PREHYDF, YDMF_PHYS_BASE_STATE%YCPG_PHY%PREHYD,                  &
  & YDMF_PHYS_BASE_STATE%YCPG_DYN%PHI, YDMF_PHYS_BASE_STATE%YCPG_DYN%PHIF, LLLAND, YDMF_PHYS_BASE_STATE%T,                  &
  & ZLU, ZMFU, YDMF_PHYS%OUT%CAPE, YDMF_PHYS%OUT%FPLCL, YDMF_PHYS%OUT%FPLCN, ZQPFROZ, LLCUM, ICBOT, ICTOP,                  &
  & LLLINOX, YDMF_PHYS%OUT%FLASH, ZLIGH_CTG, ZCTOPH, ZPRECMX, ZICE, ZCDEPTH, ZWMFU, ZCHARGE) 

! LIGHTNING FLASH RATES ARE CONVERTED IN fl/km2/s BEFORE ENTERING CFU TIME ACCUMULATION.

  DO JLON = KIDIA, KFDIA
    YDMF_PHYS%OUT%FLASH(JLON)=YDMF_PHYS%OUT%FLASH(JLON)/86400._JPRB
  ENDDO

!$ACDC }

ENDIF

END ASSOCIATE

IF (LHOOK) CALL DR_HOOK ('APL_ARPEGE_DEEP_CONVECTION', 1, ZHOOK_HANDLE)

END SUBROUTINE APL_ARPEGE_DEEP_CONVECTION
