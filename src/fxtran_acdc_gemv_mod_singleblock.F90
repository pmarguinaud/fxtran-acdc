MODULE FXTRAN_ACDC_GEMV_MOD_SINGLEBLOCK

!
! Copyright 2025 Meteo-France
! All rights reserved
! philippe.marguinaud@meteo.fr
!

IMPLICIT NONE

PRIVATE

PUBLIC :: FXTRAN_ACDC_GEMV_SINGLEBLOCK

CONTAINS

SUBROUTINE FXTRAN_ACDC_GEMV_SINGLEBLOCK (KIDIA, KFDIA, TRANSA, TRANSB, M, N, K, ALPHA, A, &
                                       & LDA, B, LDB, BETA, C, LDC, LDDONE, LDACC)
#ifdef USE_OPENACC
#if defined(_OPENACC) && (__NVCOMPILER==1)
USE CUBLAS_V2
USE CUDAFOR
#endif

USE FXTRAN_ACDC_ABORT_MOD

                                ! VERINTS
INTEGER     :: KIDIA
INTEGER     :: KFDIA
CHARACTER*1 :: TRANSA           ! 'N'
CHARACTER*1 :: TRANSB           ! 'T'
INTEGER     :: M                ! KPROMA
INTEGER     :: N                ! 1
INTEGER     :: K                ! KLEVIN
REAL*8      :: ALPHA            ! 1.0_JPRD
REAL*8      :: A (:, :)         ! ZIN
INTEGER     :: LDA              ! KPROMA
REAL*8      :: B (:, :)         ! PINTE
INTEGER     :: LDB              ! KLEVOUT
REAL*8      :: BETA             ! 0.0_JPRB
REAL*8      :: C (:)            ! POUTS
INTEGER     :: LDC              ! KPROMA
LOGICAL     :: LDDONE
LOGICAL     :: LDACC
#if (__NVCOMPILER==1)
TYPE (CUBLASHANDLE), SAVE :: YLCBH
LOGICAL, SAVE :: LLCBH_INITIALIZED = .FALSE.
INTEGER (KIND=CUDA_STREAM_KIND), SAVE :: STREAM = 0
#endif
INTEGER :: JM, JN, JK
CHARACTER*1, SAVE :: CLENV = ''
LOGICAL, SAVE :: LLSIMPLE_DGEMM = .FALSE.

IF (CLENV == '') THEN
  CALL GETENV ('FXTRAN_ACDC_SIMPLE_DGEMM', CLENV)
  LLSIMPLE_DGEMM = CLENV (1:1) == '1' 
  CLENV = '0'
ENDIF

IF (TRANSA /= 'N') CALL FXTRAN_ACDC_ABORT ('FXTRAN_ACDC_GEMM')
IF (TRANSB /= 'T') CALL FXTRAN_ACDC_ABORT ('FXTRAN_ACDC_GEMM')

CALL CHECKCONTIGUOUS2 (A)
CALL CHECKCONTIGUOUS2 (B)
CALL CHECKCONTIGUOUS1 (C)

IF (LDA /= SIZE (A, 1)) CALL FXTRAN_ACDC_ABORT ('FXTRAN_ACDC_GEMM')
IF (LDB /= SIZE (B, 1)) CALL FXTRAN_ACDC_ABORT ('FXTRAN_ACDC_GEMM')
IF (LDC /= SIZE (C, 1)) CALL FXTRAN_ACDC_ABORT ('FXTRAN_ACDC_GEMM')
IF (KIDIA /= 1) CALL FXTRAN_ACDC_ABORT ('FXTRAN_ACDC_GEMM')
IF (ALPHA /= 1._8) CALL FXTRAN_ACDC_ABORT ('FXTRAN_ACDC_GEMM')
IF (BETA /= 0._8) CALL FXTRAN_ACDC_ABORT ('FXTRAN_ACDC_GEMM')

IF (LLSIMPLE_DGEMM) THEN

  !$ACC PARALLEL LOOP GANG VECTOR &
  !$ACC&PRESENT (A, B, C) &
  !$ACC&PRIVATE (JK, JN, JM) IF(LDACC) 

  DO JM = KIDIA, KFDIA
    C (JM) = 0.
    DO JK = 1, K
      C (JM) = C (JM) + B (LDB , JK) * A (JM, JK)
    ENDDO
  ENDDO

ELSE

  IF (LDACC) THEN
#if defined(_OPENACC) && (__NVCOMPILER==1)
    IF (.NOT. LLCBH_INITIALIZED) THEN
      CALL CHECKCUBLAS (CUBLASCREATE (YLCBH))
      CALL CHECKCUBLAS (CUBLASGETSTREAM (YLCBH, STREAM))
      LLCBH_INITIALIZED = .TRUE.
    ENDIF
    !$ACC DATA PRESENT(A,B,C)
    !$ACC HOST_DATA USE_DEVICE(A,B,C)
    CALL CHECKCUBLAS(CUBLASDGEMM_V2 (YLCBH,CUBLAS_OP_N,CUBLAS_OP_T, M, N, K, ALPHA, A(1,1), LDA, B(LDB,1), LDB, BETA, C(1), LDC))
    !$ACC END HOST_DATA
    !$ACC END DATA
    CALL CHECKCUDA (CUDASTREAMSYNCHRONIZE (STREAM))
#else
    CALL DGEMM ('N','T', M, N, K, ALPHA, A(1,1), LDA, B(LDB,1), LDB, BETA, C(1), LDC)
#endif

  ELSE
    CALL DGEMM ('N','T', M, N, K, ALPHA, A(1,1), LDA, B(LDB,1), LDB, BETA, C(1), LDC)
  ENDIF

ENDIF

LDDONE = .TRUE.
#endif

#ifdef USE_OPENMP
!#if defined(__HIP_PLATFORM_AMD__)
USE HIPFORT
USE HIPFORT_ROCBLAS
USE ISO_C_BINDING
!#endif

USE FXTRAN_ACDC_ABORT_MOD

                                ! VERINTS
INTEGER     :: KIDIA
INTEGER     :: KFDIA
CHARACTER*1 :: TRANSA           ! 'N'
CHARACTER*1 :: TRANSB           ! 'T'
INTEGER     :: M                ! KPROMA
INTEGER     :: N                ! 1
INTEGER     :: K                ! KLEVIN
REAL*8      :: ALPHA            ! 1.0_JPRD
REAL*8      :: A (:, :)         ! ZIN
INTEGER     :: LDA              ! KPROMA
REAL*8      :: B (:, :)         ! PINTE
INTEGER     :: LDB              ! KLEVOUT
REAL*8      :: BETA             ! 0.0_JPRB
REAL*8      :: C (:)            ! POUTS
INTEGER     :: LDC              ! KPROMA
LOGICAL     :: LDDONE
LOGICAL     :: LDACC

TYPE (C_PTR), SAVE :: YLCBH
LOGICAL, SAVE :: LLCBH_INITIALIZED = .FALSE.
TYPE (C_PTR), SAVE :: STREAM 

INTEGER :: JM, JN, JK
CHARACTER*1, SAVE :: CLENV = ''
LOGICAL, SAVE :: LLSIMPLE_DGEMM = .FALSE.

IF (CLENV == '') THEN
  CALL GETENV ('FXTRAN_ACDC_SIMPLE_DGEMM', CLENV)
  LLSIMPLE_DGEMM = CLENV (1:1) == '1' 
  CLENV = '0'
ENDIF

IF (TRANSA /= 'N') CALL FXTRAN_ACDC_ABORT ('FXTRAN_ACDC_GEMM')
IF (TRANSB /= 'T') CALL FXTRAN_ACDC_ABORT ('FXTRAN_ACDC_GEMM')

CALL CHECKCONTIGUOUS2 (A)
CALL CHECKCONTIGUOUS2 (B)
CALL CHECKCONTIGUOUS1 (C)

IF (LDA /= SIZE (A, 1)) CALL FXTRAN_ACDC_ABORT ('FXTRAN_ACDC_GEMM')
IF (LDB /= SIZE (B, 1)) CALL FXTRAN_ACDC_ABORT ('FXTRAN_ACDC_GEMM')
IF (LDC /= SIZE (C, 1)) CALL FXTRAN_ACDC_ABORT ('FXTRAN_ACDC_GEMM')
IF (KIDIA /= 1) CALL FXTRAN_ACDC_ABORT ('FXTRAN_ACDC_GEMM')
IF (ALPHA /= 1._8) CALL FXTRAN_ACDC_ABORT ('FXTRAN_ACDC_GEMM')
IF (BETA /= 0._8) CALL FXTRAN_ACDC_ABORT ('FXTRAN_ACDC_GEMM')

IF (LLSIMPLE_DGEMM) THEN

  !$OMP TARGET DATA MAP(PRESENT: A(1:LDA,1:K),B(1:LDB,1:K),C(1:LDC)) IF(LDACC)
  !$OMP TARGET TEAMS DISTRIBUTE PARALLEL DO SIMD  &
  !$OMP& PRIVATE (JK, JN, JM) IF(LDACC) 

  DO JM = KIDIA, KFDIA
    C (JM) = 0.
    DO JK = 1, K
      C (JM) = C (JM) + B (LDB , JK) * A (JM, JK)
    ENDDO
  ENDDO
  !$OMP END TARGET TEAMS DISTRIBUTE PARALLEL DO SIMD
  !$OMP END TARGET DATA

ELSE

  IF (LDACC) THEN
!#if defined(__HIP_PLATFORM_AMD__)
#ifdef USE_OPENMP

    IF (.NOT. LLCBH_INITIALIZED) THEN
      CALL CHECKROCBLAS (ROCBLAS_CREATE_HANDLE (YLCBH))
      CALL CHECKROCBLAS(ROCBLAS_GET_STREAM(YLCBH,STREAM))     
      LLCBH_INITIALIZED = .TRUE.
    ENDIF


    !$OMP TARGET DATA MAP(PRESENT: A(1:LDA,1:K),B(1:LDB,1:K),C(1:LDC)) 
    !$OMP TARGET DATA USE_DEVICE_ADDR(A,B,C)
    CALL CHECKROCBLAS(ROCBLAS_DGEMM(YLCBH,ROCBLAS_OPERATION_NONE,ROCBLAS_OPERATION_TRANSPOSE, M, N, K, ALPHA, A(1,1), LDA, B(LDB,1), LDB, BETA, C(1), LDC))
    !$OMP END TARGET DATA
    !$OMP END TARGET DATA
    CALL CHECKHIP(HIPSTREAMSYNCHRONIZE(STREAM))


#else
!    CALL DGEMM ('N','T', M, N, K, ALPHA, A(1,1), LDA, B(LDB,1), LDB, BETA, C(1), LDC)
    CALL FXTRAN_ACDC_ABORT ('FXTRAN_ACDC_GEMV_SINGLEBLOCK')
#endif

  ELSE
    CALL DGEMM ('N','T', M, N, K, ALPHA, A(1,1), LDA, B(LDB,1), LDB, BETA, C(1), LDC)
  ENDIF

ENDIF

LDDONE = .TRUE.
#endif

END SUBROUTINE

#if defined(USE_OPENACC) && (__NVCOMPILER==1)
SUBROUTINE CHECKCUBLAS (STATUS)

USE CUBLAS_V2

USE FXTRAN_ACDC_ABORT_MOD

INTEGER :: STATUS

IF (STATUS /= CUBLAS_STATUS_SUCCESS) THEN
  PRINT *, 'CUBLAS ERROR: STATUS =', STATUS
  CALL FXTRAN_ACDC_ABORT ('FXTRAN_ACDC_GEMM')
END IF

END SUBROUTINE CHECKCUBLAS

SUBROUTINE CHECKCUDA (STATUS)

USE CUDAFOR

USE FXTRAN_ACDC_ABORT_MOD

INTEGER :: STATUS

IF (STATUS /= CUDASUCCESS) THEN
  PRINT *, 'CUDA ERROR:', CUDAGETERRORSTRING (STATUS)
  CALL FXTRAN_ACDC_ABORT ('FXTRAN_ACDC_GEMM')
END IF

END SUBROUTINE CHECKCUDA
#endif


#ifdef USE_OPENMP
SUBROUTINE CHECKROCBLAS (STATUS)

USE HIPFORT
USE HIPFORT_ROCBLAS

USE FXTRAN_ACDC_ABORT_MOD

INTEGER :: STATUS

IF (STATUS /= ROCBLAS_STATUS_SUCCESS) THEN
  PRINT *, 'ROCBLAS ERROR: STATUS =', STATUS
  CALL FXTRAN_ACDC_ABORT ('FXTRAN_ACDC_GEMM')
END IF

END SUBROUTINE CHECKROCBLAS

SUBROUTINE CHECKHIP (STATUS)

USE HIPFORT

USE FXTRAN_ACDC_ABORT_MOD

INTEGER :: STATUS

IF (STATUS /= HIPSUCCESS) THEN
!  PRINT *, 'HIP ERROR:', HIPGETERRORSTRING (STATUS)
  PRINT *, 'HIP ERROR:', STATUS
  CALL FXTRAN_ACDC_ABORT ('FXTRAN_ACDC_GEMM')
END IF

END SUBROUTINE CHECKHIP
#endif


SUBROUTINE CHECKCONTIGUOUS1 (P)

USE FXTRAN_ACDC_ABORT_MOD

REAL*8 :: P (:)
IF (LOC (P (2)) - LOC (P (1)) /= 8) CALL FXTRAN_ACDC_ABORT ('FXTRAN_ACDC_GEMM')
END SUBROUTINE

SUBROUTINE CHECKCONTIGUOUS2 (P)

USE FXTRAN_ACDC_ABORT_MOD

REAL*8 :: P (:, :)
IF (LOC (P (1, 2)) - LOC (P (1, 1)) /= SIZE (P, 1) * 8) CALL FXTRAN_ACDC_ABORT ('FXTRAN_ACDC_GEMM')
END SUBROUTINE

END MODULE
