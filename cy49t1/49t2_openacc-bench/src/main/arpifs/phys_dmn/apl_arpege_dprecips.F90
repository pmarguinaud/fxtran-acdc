SUBROUTINE APL_ARPEGE_DPRECIPS (YDMF_PHYS_BASE_STATE, YDCPG_BNDS, YDCPG_OPTS, YDMF_PHYS, YDVARS, YDMODEL, PPRC_DPRECIPS, PPRC_DPRECIPS2)

!**** *APL_ARPEGE_DPRECIPS*

!     Author. 
!     ------- 
!      Philippe Marguinaud *METEO-FRANCE*
!      Original : 28-04-2021

!      R. El Khatib 26-Apr-2023 Loop pushing in ppwetpoint

USE PARKIND1, ONLY : JPIM, JPRB
USE YOMHOOK, ONLY : LHOOK, DR_HOOK, JPHOOK

USE MF_PHYS_BASE_STATE_TYPE_MOD &
                       , ONLY : MF_PHYS_BASE_STATE_TYPE
USE CPG_OPTS_TYPE_MOD  , ONLY : CPG_BNDS_TYPE, CPG_OPTS_TYPE
USE MF_PHYS_TYPE_MOD   , ONLY : MF_PHYS_TYPE
USE FIELD_VARIABLES_MOD, ONLY : FIELD_VARIABLES
USE TYPE_MODEL         , ONLY : MODEL


IMPLICIT NONE

TYPE(MF_PHYS_BASE_STATE_TYPE),  INTENT(IN)    :: YDMF_PHYS_BASE_STATE
TYPE(CPG_BNDS_TYPE),            INTENT(IN)    :: YDCPG_BNDS
TYPE(CPG_OPTS_TYPE),            INTENT(IN)    :: YDCPG_OPTS
TYPE(MF_PHYS_TYPE),             INTENT(INOUT) :: YDMF_PHYS
TYPE(FIELD_VARIABLES),          INTENT(INOUT) :: YDVARS
TYPE(MODEL),                    INTENT(IN)    :: YDMODEL
REAL(KIND=JPRB),                INTENT(OUT)   :: PPRC_DPRECIPS  (YDCPG_OPTS%KLON, 1:YDCPG_OPTS%NDTPREC)
REAL(KIND=JPRB),                INTENT(OUT)   :: PPRC_DPRECIPS2 (YDCPG_OPTS%KLON, 1:YDCPG_OPTS%NDTPREC2)


#include "dprecips.intfb.h"
#include "ppwetpoint.intfb.h"

REAL(KIND=JPRB) :: ZDZZ(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZFPLC(YDCPG_OPTS%KLON)
REAL(KIND=JPRB) :: ZFPLS(YDCPG_OPTS%KLON)
REAL(KIND=JPRB) :: ZFPLSG(YDCPG_OPTS%KLON)
REAL(KIND=JPRB) :: ZTW(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZZZ (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)

REAL(KIND=JPRB) :: ZINVG
INTEGER (KIND=JPIM) :: JLEV
INTEGER (KIND=JPIM) :: JLON

REAL (KIND=JPHOOK) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK ('APL_ARPEGE_DPRECIPS', 0, ZHOOK_HANDLE)

ZINVG = 1._JPRB/YDMODEL%YRCST%RG

ASSOCIATE (KLON => YDCPG_OPTS%KLON, KIDIA => YDCPG_BNDS%KIDIA, KFDIA => YDCPG_BNDS%KFDIA, KFLEVG => YDCPG_OPTS%KFLEVG)

!$ACDC PARALLEL, TARGET=OpenMP/OpenMPSingleColumn/OpenACCSingleColumn {

! Compute wet-bulb temperature

  CALL PPWETPOINT(YDMODEL%YRCST, YDMODEL%YRML_PHY_MF%YRPHY, KIDIA, KFDIA, KLON, KFLEVG,                        &
  & YDMF_PHYS_BASE_STATE%YCPG_PHY%PREHYDF(:, :), YDMF_PHYS_BASE_STATE%T(:, :), YDMF_PHYS_BASE_STATE%Q(:, :),   &
  & YDMF_PHYS_BASE_STATE%L(:, :), YDMF_PHYS_BASE_STATE%I(:, :), ZTW(:, :))

DO JLON = KIDIA, KFDIA
  ZFPLS(JLON)=YDMF_PHYS%OUT%FPLCN(JLON,KFLEVG)+YDMF_PHYS%OUT%FPLSN(JLON,KFLEVG)
  ZFPLC(JLON)=YDMF_PHYS%OUT%FPLCL(JLON,KFLEVG)+YDMF_PHYS%OUT%FPLSL(JLON,KFLEVG)
  ZFPLSG(JLON)=0._JPRB
ENDDO

! Initialisation de ZZZ
DO JLEV = 1,KFLEVG
  DO JLON = KIDIA, KFDIA
    ZZZ(JLON,JLEV)=YDMF_PHYS_BASE_STATE%YCPG_DYN%PHI(JLON,JLEV)*ZINVG
  ENDDO
ENDDO

! Initialisation de ZDZZ
DO JLEV = 2, KFLEVG
  DO JLON = KIDIA, KFDIA
    ZDZZ(JLON,JLEV)=ZZZ(JLON,JLEV-1)-ZZZ(JLON,JLEV)
  ENDDO
ENDDO

DO JLON = KIDIA, KFDIA
  ZDZZ(JLON,1)=YDMF_PHYS_BASE_STATE%YCPG_DYN%PHI(JLON,0)*ZINVG-ZZZ(JLON,1)
ENDDO

CALL DPRECIPS (YDMODEL%YRCST, YDMODEL%YRML_PHY_MF%YRPHY%YRDPRECIPS, KIDIA, KFDIA, KLON, KFLEVG,                                               &
& YDVARS%GEOMETRY%OROG%T0, YDMF_PHYS%OUT%TPWCLS, YDMF_PHYS%OUT%DIAGH, YDMF_PHYS_BASE_STATE%YCPG_DYN%PHIF, ZDZZ, ZTW, YDMF_PHYS_BASE_STATE%L,  &
& ZFPLC, ZFPLS, ZFPLSG, PPRC_DPRECIPS(:, YDCPG_OPTS%NDTPRECCUR))
  
! Idem for an other time step and an other period

CALL DPRECIPS(YDMODEL%YRCST, YDMODEL%YRML_PHY_MF%YRPHY%YRDPRECIPS, KIDIA, KFDIA, KLON, KFLEVG,                                                &
& YDVARS%GEOMETRY%OROG%T0, YDMF_PHYS%OUT%TPWCLS, YDMF_PHYS%OUT%DIAGH, YDMF_PHYS_BASE_STATE%YCPG_DYN%PHIF, ZDZZ, ZTW, YDMF_PHYS_BASE_STATE%L,  &
& ZFPLC, ZFPLS, ZFPLSG, PPRC_DPRECIPS2(:, YDCPG_OPTS%NDTPRECCUR2))

!$ACDC }

END ASSOCIATE

IF (LHOOK) CALL DR_HOOK ('APL_ARPEGE_DPRECIPS', 1, ZHOOK_HANDLE)

END SUBROUTINE APL_ARPEGE_DPRECIPS

