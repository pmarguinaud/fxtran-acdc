!OPTIONS XOPT(NOEVAL)
SUBROUTINE ACRANEB_SOLVT (YDPHY,KIDIA,KFDIA,KLON,KTDIA,KLEV,&
! - INPUT 1D
 & PEMIS,&
! - INPUT 2D
 & PNEB,PB1,PB2,PB3,PB4,&
! - INPUT/OUTPUT 2D
 & PA4C,PA5C,PA4N,PA5N,&
 & PFDC,PFMC,PFDN,PFMN)

! Purpose:
! --------
!   ACRANEB_SOLVT - Adding system solver (thermal band).

! Interface:
! ----------
! INPUT:
!   KIDIA - initial index for horizontal loops
!   KFDIA - final index for horizontal loops
!   KLON  - horizontal dimension of arrays
!   KTDIA - initial index for vertical loops (usually 1)
!   KLEV  - vertical dimension of full level arrays
!   PEMIS - surface emissivity
!   PNEB  - cloud fraction
!   PB1   - 1st intermediate storage for cloud geometry
!   PB2   - 2nd intermediate storage for cloud geometry
!   PB3   - 3rd intermediate storage for cloud geometry
!   PB4   - 4th intermediate storage for cloud geometry
! INPUT/OUTPUT:
!   PA4C  - clearsky diffuse transmissivity
!   PA5C  - clearsky diffuse reflectivity
!   PA4N  - cloudy diffuse transmissivity
!   PA5N  - cloudy diffuse reflectivity
!   PFDC  - right hand side term for clearsky downwards diffusive flux
!   PFDN  - right hand side term for cloudy downwards difffusive flux
!   PFMC  - right hand side term for clearsky upwards difffusive flux
!   PFMN  - right hand side term for cloudy upwards difffusive flux

! Externals:
! ----------

! Method:
! -------

! Reference:
! ----------

! Author:
! -------
!   1989-12, J.-F. Geleyn (original ACRANEB)

! Modifications:
! --------------
!   2009-10, T. Kral
!   Externalized from ACRANEB.
!
!   2013-11, J. Masek
!   Phasing to cy40t1.
! End Modifications
!-----------------------------------------------------------------------

USE PARKIND1  ,ONLY : JPIM,    JPRB
USE YOMPHY    ,ONLY : TPHY

IMPLICIT NONE

TYPE(TPHY)        ,INTENT(INOUT):: YDPHY
INTEGER(KIND=JPIM),INTENT(IN)  :: KLON 
INTEGER(KIND=JPIM),INTENT(IN)  :: KLEV 
INTEGER(KIND=JPIM),INTENT(IN)  :: KIDIA 
INTEGER(KIND=JPIM),INTENT(IN)  :: KFDIA 
INTEGER(KIND=JPIM),INTENT(IN)  :: KTDIA 

REAL(KIND=JPRB),INTENT(IN)     :: PEMIS(KLON)
REAL(KIND=JPRB),INTENT(IN)     :: PNEB(KLON,KLEV)
REAL(KIND=JPRB),INTENT(IN)     :: PB1(KLON,KLEV)
REAL(KIND=JPRB),INTENT(IN)     :: PB2(KLON,KLEV)
REAL(KIND=JPRB),INTENT(IN)     :: PB3(KLON,KLEV)
REAL(KIND=JPRB),INTENT(IN)     :: PB4(KLON,KLEV)
REAL(KIND=JPRB),INTENT(INOUT)  :: PA4C(KLON,KLEV)
REAL(KIND=JPRB),INTENT(INOUT)  :: PA5C(KLON,KLEV)
REAL(KIND=JPRB),INTENT(INOUT)  :: PA4N(KLON,KLEV)
REAL(KIND=JPRB),INTENT(INOUT)  :: PA5N(KLON,KLEV)
REAL(KIND=JPRB),INTENT(INOUT)  :: PFDC(KLON,0:KLEV)
REAL(KIND=JPRB),INTENT(INOUT)  :: PFMC(KLON,0:KLEV)
REAL(KIND=JPRB),INTENT(INOUT)  :: PFDN(KLON,0:KLEV)
REAL(KIND=JPRB),INTENT(INOUT)  :: PFMN(KLON,0:KLEV)

#include "abor1.intfb.h"

!-----------------------------------------------------------------------

!     LOCAL REAL SCALARS
REAL(KIND=JPRB) :: ZAL,ZTD1,ZTD2,ZTD3,ZTD4,ZTD5,ZTD6,ZTD7,ZTDS1,ZTDS2,&
                 & ZTDS3,ZTUS1

!     LOCAL REAL 1D ARRAYS
REAL(KIND=JPRB) :: ZAL1(KLON),ZAL2(KLON),ZBE1(KLON),ZBE2(KLON),ZDE1(KLON),&
                 & ZDE2(KLON),ZGA1(KLON),ZGA2(KLON)

!     LOCAL REAL 2D ARRAYS
REAL(KIND=JPRB) :: ZTU1(KLON,KLEV),ZTU2(KLON,KLEV),ZTU3(KLON,KLEV),&
                 & ZTU4(KLON,KLEV),ZTU5(KLON,KLEV),ZTU6(KLON,KLEV),&
                 & ZTU7(KLON,KLEV),ZTU8(KLON,KLEV),ZTU9(KLON,KLEV)

!     LOCAL INTEGER SCALARS
INTEGER(KIND=JPIM) :: JLEV, JLON

!-----------------------------------------------------------------------

ASSOCIATE(LRNUMX=>YDPHY%LRNUMX)
!-----------------------------------------------------------------------

! - TEMPORAIRE(S) 1D

! ZAL1      : WEIGHT OF THE M OUTGOING C FLUX IN THE M INGOING C FLUX.
! ZAL2      : WEIGHT OF THE D OUTGOING C FLUX IN THE D INGOING C FLUX.
! ZBE1      : WEIGHT OF THE M OUTGOING C FLUX IN THE M INGOING N FLUX.
! ZBE2      : WEIGHT OF THE D OUTGOING C FLUX IN THE D INGOING N FLUX.
! ZGA1      : WEIGHT OF THE M OUTGOING N FLUX IN THE M INGOING C FLUX.
! ZGA2      : WEIGHT OF THE D OUTGOING N FLUX IN THE D INGOING C FLUX.
! ZDE1      : WEIGHT OF THE M OUTGOING N FLUX IN THE M INGOING N FLUX.
! ZDE2      : WEIGHT OF THE D OUTGOING N FLUX IN THE D INGOING N FLUX.

! - TEMPORAIRE(S) 2D (1:KLEV)

! ZTU1      : 1ST SUPERDIAGONAL COEFF. FOR THE ADDING SYSTEM.
! ZTU2      : 2ND SUPERDIAGONAL COEFF. FOR THE ADDING SYSTEM.
! ZTU3      : 3RD SUPERDIAGONAL COEFF. FOR THE ADDING SYSTEM.
! ZTU4      : 4TH SUPERDIAGONAL COEFF. FOR THE ADDING SYSTEM.
! ZTU5      : 5TH SUPERDIAGONAL COEFF. FOR THE ADDING SYSTEM.
! ZTU6      : 6TH SUPERDIAGONAL COEFF. FOR THE ADDING SYSTEM.
! ZTU7      : 7TH SUPERDIAGONAL COEFF. FOR THE ADDING SYSTEM.
! ZTU8      : 8TH SUPERDIAGONAL COEFF. FOR THE ADDING SYSTEM.
! ZTU9      : 9TH SUPERDIAGONAL COEFF. FOR THE ADDING SYSTEM.

! I.1 - FIRST LAYER, ELIMINATION (EASY).
!-------------------------------------------------------------------------------

DO JLON=KIDIA,KFDIA
  IF (LRNUMX) THEN
    ZAL1(JLON)=PB2(JLON,KTDIA)
    ZAL2(JLON)=PB1(JLON,KTDIA)
    ZBE1(JLON)=1._JPRB-PB2(JLON,KTDIA)
    ZBE2(JLON)=1._JPRB-PB1(JLON,KTDIA)
    ZGA1(JLON)=1._JPRB-PB4(JLON,KTDIA)
    ZGA2(JLON)=1._JPRB-PB3(JLON,KTDIA)
    ZDE1(JLON)=PB4(JLON,KTDIA)
    ZDE2(JLON)=PB3(JLON,KTDIA)
  ELSE
    PA4C(JLON,KTDIA)=PA4C(JLON,KTDIA)*PB1(JLON,KTDIA)&
     & +PA4N(JLON,KTDIA)*PNEB(JLON,KTDIA)
    PA5C(JLON,KTDIA)=PA5C(JLON,KTDIA)*PB1(JLON,KTDIA)&
     & +PA5N(JLON,KTDIA)*PNEB(JLON,KTDIA)
  ENDIF
ENDDO

DO JLON=KIDIA,KFDIA

  IF (LRNUMX) THEN
    PFMC(JLON,KTDIA-1)=PA5C(JLON,KTDIA)*(ZAL2(JLON)&
     & *PFDC(JLON,KTDIA-1))+PFMC(JLON,KTDIA-1)
    PFDC(JLON,KTDIA)=PA4C(JLON,KTDIA)*(ZAL2(JLON)&
     & *PFDC(JLON,KTDIA-1))+PFDC(JLON,KTDIA)
    PFMN(JLON,KTDIA-1)=PA5N(JLON,KTDIA)*(ZBE2(JLON)&
     & *PFDC(JLON,KTDIA-1))+PFMN(JLON,KTDIA-1)
    PFDN(JLON,KTDIA)=PA4N(JLON,KTDIA)*(ZBE2(JLON)&
     & *PFDC(JLON,KTDIA-1))+PFDN(JLON,KTDIA)
    ZTU1(JLON,KTDIA)=0._JPRB
    ZTU2(JLON,KTDIA)=ZAL1(JLON)*PA4C(JLON,KTDIA)
    ZTU3(JLON,KTDIA)=ZGA1(JLON)*PA4C(JLON,KTDIA)
    ZTU4(JLON,KTDIA)=ZBE1(JLON)*PA4N(JLON,KTDIA)
    ZTU5(JLON,KTDIA)=ZDE1(JLON)*PA4N(JLON,KTDIA)
    ZTU6(JLON,KTDIA)=ZAL1(JLON)*PA5C(JLON,KTDIA)
    ZTU7(JLON,KTDIA)=ZGA1(JLON)*PA5C(JLON,KTDIA)
    ZTU8(JLON,KTDIA)=ZBE1(JLON)*PA5N(JLON,KTDIA)
    ZTU9(JLON,KTDIA)=ZDE1(JLON)*PA5N(JLON,KTDIA)
  ELSE
    PFMC(JLON,KTDIA-1)=PA5C(JLON,KTDIA)*PFDC(JLON,KTDIA-1)&
     & +PFMC(JLON,KTDIA-1)
    PFDC(JLON,KTDIA)=PA4C(JLON,KTDIA)*PFDC(JLON,KTDIA-1)&
     & +PFDC(JLON,KTDIA)
    ZTU2(JLON,KTDIA)=PA4C(JLON,KTDIA)
    ZTU6(JLON,KTDIA)=PA5C(JLON,KTDIA)
  ENDIF

ENDDO

! I.2 - LOOP OVER THE LAYERS, PRELIMINARY COMPUTATIONS AND THEN ELIMINATION.
!-------------------------------------------------------------------------------

DO JLEV=KTDIA+1,KLEV

  DO JLON=KIDIA,KFDIA
    IF (LRNUMX) THEN
      ZAL1(JLON)=PB2(JLON,JLEV)
      ZAL2(JLON)=PB1(JLON,JLEV)
      ZBE1(JLON)=1._JPRB-PB2(JLON,JLEV)
      ZBE2(JLON)=1._JPRB-PB1(JLON,JLEV)
      ZGA1(JLON)=1._JPRB-PB4(JLON,JLEV)
      ZGA2(JLON)=1._JPRB-PB3(JLON,JLEV)
      ZDE1(JLON)=PB4(JLON,JLEV)
      ZDE2(JLON)=PB3(JLON,JLEV)
    ELSE
      PA4C(JLON,JLEV)=PA4C(JLON,JLEV)*PB1(JLON,JLEV)&
       & +PA4N(JLON,JLEV)*PNEB(JLON,JLEV)
      PA5C(JLON,JLEV)=PA5C(JLON,JLEV)*PB1(JLON,JLEV)&
       & +PA5N(JLON,JLEV)*PNEB(JLON,JLEV)
    ENDIF
  ENDDO

  DO JLON=KIDIA,KFDIA

    IF (LRNUMX) THEN
      ZTD1=1._JPRB/(1._JPRB-PA5C(JLON,JLEV)*(ZAL2(JLON)&
       & *ZTU6(JLON,JLEV-1)+ZGA2(JLON)*ZTU8(JLON,JLEV-1)))
      PFMC(JLON,JLEV-1)=(ZTD1*PA5C(JLON,JLEV))*(ZAL2(JLON)&
       & *PFDC(JLON,JLEV-1)+ZGA2(JLON)&
       & *PFDN(JLON,JLEV-1))+ZTD1*PFMC(JLON,JLEV-1)
      ZTU1(JLON,JLEV)=(ZTD1*PA5C(JLON,JLEV))*(ZAL2(JLON)&
       & *ZTU7(JLON,JLEV-1)+ZGA2(JLON)*ZTU9(JLON,JLEV-1))
      ZTU2(JLON,JLEV)=(ZTD1*PA4C(JLON,JLEV))*ZAL1(JLON)
      ZTU3(JLON,JLEV)=(ZTD1*PA4C(JLON,JLEV))*ZGA1(JLON)
      ZTD2=PA5N(JLON,JLEV)*(ZBE2(JLON)*ZTU6(JLON,JLEV-1)&
       & +ZDE2(JLON)*ZTU8(JLON,JLEV-1))
      ZTD3=1._JPRB/(1._JPRB-PA5N(JLON,JLEV)*(ZBE2(JLON)*ZTU7(JLON,JLEV-1)&
       & +ZDE2(JLON)*ZTU9(JLON,JLEV-1))-ZTD2*ZTU1(JLON,JLEV))
      PFMN(JLON,JLEV-1)=ZTD3*(PA5N(JLON,JLEV)*(ZBE2(JLON)&
       & *PFDC(JLON,JLEV-1)+ZDE2(JLON)*PFDN(JLON,JLEV-1))&
       & +ZTD2*PFMC(JLON,JLEV-1))+ZTD3*PFMN(JLON,JLEV-1)
      ZTU4(JLON,JLEV)=ZTD3*(PA4N(JLON,JLEV)*ZBE1(JLON)&
       & +ZTD2*ZTU2(JLON,JLEV))
      ZTU5(JLON,JLEV)=ZTD3*(PA4N(JLON,JLEV)*ZDE1(JLON)&
       & +ZTD2*ZTU3(JLON,JLEV))
      ZTD4=PA4C(JLON,JLEV)*(ZAL2(JLON)*ZTU6(JLON,JLEV-1)&
       & +ZGA2(JLON)*ZTU8(JLON,JLEV-1))
      ZTD5=PA4C(JLON,JLEV)*(ZAL2(JLON)*ZTU7(JLON,JLEV-1)&
       & +ZGA2(JLON)*ZTU9(JLON,JLEV-1))
      PFDC(JLON,JLEV)=PA4C(JLON,JLEV)*(ZAL2(JLON)*PFDC(JLON,JLEV-1)&
       & +ZGA2(JLON)*PFDN(JLON,JLEV-1))+ZTD4*PFMC(JLON,JLEV-1)&
       & +ZTD5*PFMN(JLON,JLEV-1)+PFDC(JLON,JLEV)
      ZTU6(JLON,JLEV)=PA5C(JLON,JLEV)*ZAL1(JLON)+ZTD4&
       & *ZTU2(JLON,JLEV)+ZTD5*ZTU4(JLON,JLEV)
      ZTU7(JLON,JLEV)=PA5C(JLON,JLEV)*ZGA1(JLON)+ZTD4&
       & *ZTU3(JLON,JLEV)+ZTD5*ZTU5(JLON,JLEV)
      ZTD6=PA4N(JLON,JLEV)*(ZBE2(JLON)*ZTU6(JLON,JLEV-1)&
       & +ZDE2(JLON)*ZTU8(JLON,JLEV-1))
      ZTD7=PA4N(JLON,JLEV)*(ZBE2(JLON)*ZTU7(JLON,JLEV-1)&
       & +ZDE2(JLON)*ZTU9(JLON,JLEV-1))
      PFDN(JLON,JLEV)=PA4N(JLON,JLEV)*(ZBE2(JLON)*PFDC(JLON,JLEV-1)&
       & +ZDE2(JLON)*PFDN(JLON,JLEV-1))+ZTD6*PFMC(JLON,JLEV-1)&
       & +ZTD7*PFMN(JLON,JLEV-1)+PFDN(JLON,JLEV)
      ZTU8(JLON,JLEV)=PA5N(JLON,JLEV)*ZBE1(JLON)+ZTD6&
       & *ZTU2(JLON,JLEV)+ZTD7*ZTU4(JLON,JLEV)
      ZTU9(JLON,JLEV)=PA5N(JLON,JLEV)*ZDE1(JLON)+ZTD6&
       & *ZTU3(JLON,JLEV)+ZTD7*ZTU5(JLON,JLEV)
    ELSE
      ZTD1=1._JPRB/(1._JPRB-PA5C(JLON,JLEV)*ZTU6(JLON,JLEV-1))
      PFMC(JLON,JLEV-1)=ZTD1*PA5C(JLON,JLEV)*PFDC(JLON,JLEV-1)&
       & +ZTD1*PFMC(JLON,JLEV-1)
      ZTU2(JLON,JLEV)=ZTD1*PA4C(JLON,JLEV)
      ZTD4=PA4C(JLON,JLEV)*ZTU6(JLON,JLEV-1)
      PFDC(JLON,JLEV)=PA4C(JLON,JLEV)*PFDC(JLON,JLEV-1)&
       & +ZTD4*PFMC(JLON,JLEV-1)+PFDC(JLON,JLEV)
      ZTU6(JLON,JLEV)=PA5C(JLON,JLEV)+ZTD4*ZTU2(JLON,JLEV)
    ENDIF ! LRNUMX

  ENDDO
ENDDO

! I.3 - SURFACE TREATMENT, ELIMINATION AND BACK-SUBSTITUTION.
!-------------------------------------------------------------------------------


! TODO: INCLUDE EMISSIVITY IN ZAC(N)3 & ZAC(N)5 ARRAYS, THIS REQUIRES TO 
!       INCREASE DIMENSION OF ZAC(N) TO 0:KLEV

DO JLON=KIDIA,KFDIA

  ZAL=1._JPRB-PEMIS(JLON)

  IF (LRNUMX) THEN
    ZTDS1=1._JPRB/(1._JPRB-ZAL*ZTU6(JLON,KLEV))
    PFMC(JLON,KLEV)=ZTDS1*(ZAL*PFDC(JLON,KLEV)+PFMC(JLON,KLEV))
    ZTUS1=(ZTDS1*ZAL)*ZTU7(JLON,KLEV)
    ZTDS2=ZAL*ZTU8(JLON,KLEV)
    ZTDS3=1._JPRB/(1._JPRB-ZAL*ZTU9(JLON,KLEV)-ZTDS2*ZTUS1)
    PFMN(JLON,KLEV)=ZTDS3*(ZAL*PFDN(JLON,KLEV)+ZTDS2&
     & *PFMC(JLON,KLEV)+PFMN(JLON,KLEV))
    PFMC(JLON,KLEV)=PFMC(JLON,KLEV)+ZTUS1*PFMN(JLON,KLEV)
  ELSE
    ZTDS1=1._JPRB/(1._JPRB-ZAL*ZTU6(JLON,KLEV))
    PFMC(JLON,KLEV)=ZTDS1*(ZAL*PFDC(JLON,KLEV)+PFMC(JLON,KLEV))
  ENDIF

ENDDO

! I.4 - BACK-SUBSTITUTION LAYER BY LAYER.
!-------------------------------------------------------------------------------

!cdir unroll=8
DO JLEV=KLEV,KTDIA,-1
  DO JLON=KIDIA,KFDIA

    IF (LRNUMX) THEN
      PFDN(JLON,JLEV)=PFDN(JLON,JLEV)+ZTU8(JLON,JLEV)&
       & *PFMC(JLON,JLEV)+ZTU9(JLON,JLEV)*PFMN(JLON,JLEV)
      PFDC(JLON,JLEV)=PFDC(JLON,JLEV)+ZTU6(JLON,JLEV)&
       & *PFMC(JLON,JLEV)+ZTU7(JLON,JLEV)*PFMN(JLON,JLEV)
      PFMN(JLON,JLEV-1)=PFMN(JLON,JLEV-1)+ZTU4(JLON,JLEV)&
       & *PFMC(JLON,JLEV)+ZTU5(JLON,JLEV)*PFMN(JLON,JLEV)
      PFMC(JLON,JLEV-1)=PFMC(JLON,JLEV-1)+ZTU2(JLON,JLEV)&
       & *PFMC(JLON,JLEV)+ZTU3(JLON,JLEV)*PFMN(JLON,JLEV)&
       & +ZTU1(JLON,JLEV)*PFMN(JLON,JLEV-1)
    ELSE
      PFDC(JLON,JLEV)=PFDC(JLON,JLEV)+ZTU6(JLON,JLEV)&
       & *PFMC(JLON,JLEV)
      PFMC(JLON,JLEV-1)=PFMC(JLON,JLEV-1)+ZTU2(JLON,JLEV)&
       & *PFMC(JLON,JLEV)
    ENDIF

  ENDDO
ENDDO

END ASSOCIATE
END SUBROUTINE ACRANEB_SOLVT
