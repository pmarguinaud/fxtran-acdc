SUBROUTINE GPMPFC_EXPL_PART1 (YDML_GCONF, YDDYN, YDDYNA, KPROMA,    &
& KFLEV, KST, KEND, PGM, P0U, P0V, P0DIV, P0TL, P0TM, P9U, P9V, P0UL, P0VL, P0VOR, P0SPDL, P0SPDM, &
& P0SVDL, P0SVDM, P0NHXL, P0NHXM, P9DIV, P9TL, P9TM, P9SPDL, P9SPDM, P9SVDL, P9SVDM, P0SPL, P0SPM, &
& P9SPL, P9SPM)

!**** *GPMPFC_EXPL_PART1 *

!     Author.
!     -------
!      Philippe Marguinaud *METEO-FRANCE*
!      Original : 28-11-2022

USE PARKIND1, ONLY : JPIM, JPRB
USE YOMHOOK, ONLY : DR_HOOK, LHOOK, JPHOOK

USE MODEL_GENERAL_CONF_MOD , ONLY : MODEL_GENERAL_CONF_TYPE
USE YOMDYN                 , ONLY : TDYN
USE YOMDYNA                , ONLY : TDYNA




IMPLICIT NONE

TYPE(MODEL_GENERAL_CONF_TYPE) ,INTENT(IN)     :: YDML_GCONF
TYPE(TDYN)                    ,INTENT(IN)     :: YDDYN
TYPE(TDYNA)                   ,INTENT(IN)     :: YDDYNA
INTEGER(KIND=JPIM)            ,INTENT(IN)     :: KPROMA
INTEGER(KIND=JPIM)            ,INTENT(IN)     :: KFLEV
INTEGER(KIND=JPIM)            ,INTENT(IN)     :: KST
INTEGER(KIND=JPIM)            ,INTENT(IN)     :: KEND
REAL(KIND=JPRB)               ,INTENT(IN)     :: PGM(KPROMA)                 
REAL(KIND=JPRB)               ,INTENT(INOUT)  :: P0U(KPROMA,KFLEV)
REAL(KIND=JPRB)               ,INTENT(INOUT)  :: P0V(KPROMA,KFLEV)
REAL(KIND=JPRB)               ,INTENT(INOUT)  :: P0DIV(KPROMA,KFLEV)
REAL(KIND=JPRB)               ,INTENT(INOUT)  :: P0TL(KPROMA,KFLEV)
REAL(KIND=JPRB)               ,INTENT(INOUT)  :: P0TM(KPROMA,KFLEV)
REAL(KIND=JPRB)               ,INTENT(INOUT)  :: P9U(KPROMA,KFLEV)
REAL(KIND=JPRB)               ,INTENT(INOUT)  :: P9V(KPROMA,KFLEV)
REAL(KIND=JPRB)               ,INTENT(INOUT)  :: P0UL(KPROMA,KFLEV)
REAL(KIND=JPRB)               ,INTENT(INOUT)  :: P0VL(KPROMA,KFLEV)
REAL(KIND=JPRB)               ,INTENT(INOUT)  :: P0VOR(KPROMA,KFLEV)
REAL(KIND=JPRB)               ,INTENT(INOUT)  :: P0SPDL(KPROMA,KFLEV)
REAL(KIND=JPRB)               ,INTENT(INOUT)  :: P0SPDM(KPROMA,KFLEV)
REAL(KIND=JPRB)               ,INTENT(INOUT)  :: P0SVDL(KPROMA,KFLEV)
REAL(KIND=JPRB)               ,INTENT(INOUT)  :: P0SVDM(KPROMA,KFLEV)
REAL(KIND=JPRB)               ,INTENT(INOUT)  :: P0NHXL(KPROMA,KFLEV)
REAL(KIND=JPRB)               ,INTENT(INOUT)  :: P0NHXM(KPROMA,KFLEV)
REAL(KIND=JPRB)               ,INTENT(INOUT)  :: P9DIV(KPROMA,KFLEV)
REAL(KIND=JPRB)               ,INTENT(INOUT)  :: P9TL(KPROMA,KFLEV)
REAL(KIND=JPRB)               ,INTENT(INOUT)  :: P9TM(KPROMA,KFLEV)
REAL(KIND=JPRB)               ,INTENT(INOUT)  :: P9SPDL(KPROMA,KFLEV)
REAL(KIND=JPRB)               ,INTENT(INOUT)  :: P9SPDM(KPROMA,KFLEV)
REAL(KIND=JPRB)               ,INTENT(INOUT)  :: P9SVDL(KPROMA,KFLEV)
REAL(KIND=JPRB)               ,INTENT(INOUT)  :: P9SVDM(KPROMA,KFLEV)
REAL(KIND=JPRB)               ,INTENT(INOUT)  :: P0SPL(KPROMA)      
REAL(KIND=JPRB)               ,INTENT(INOUT)  :: P0SPM(KPROMA)      
REAL(KIND=JPRB)               ,INTENT(INOUT)  :: P9SPL(KPROMA)      
REAL(KIND=JPRB)               ,INTENT(INOUT)  :: P9SPM(KPROMA)      

INTEGER (KIND=JPIM) :: JLEV
INTEGER (KIND=JPIM) :: JROF

REAL (KIND=JPHOOK) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('GPMPFC_EXPL_PART1',0,ZHOOK_HANDLE)

! * GMV (unconditional t0 and t9 merged):

DO JLEV=1,KFLEV
  DO JROF=KST,KEND
    ! mapping of gmv t0
    P0U(JROF,JLEV)=P0U(JROF,JLEV)*PGM(JROF)
    P0V(JROF,JLEV)=P0V(JROF,JLEV)*PGM(JROF)
    P0DIV(JROF,JLEV)=P0DIV(JROF,JLEV)*PGM(JROF)*PGM(JROF)
    P0TL(JROF,JLEV)=P0TL(JROF,JLEV)*PGM(JROF)
    P0TM(JROF,JLEV)=P0TM(JROF,JLEV)*PGM(JROF)

    ! mapping of gmv t9
    P9U(JROF,JLEV)=P9U(JROF,JLEV)*PGM(JROF)
    P9V(JROF,JLEV)=P9V(JROF,JLEV)*PGM(JROF)
  ENDDO

  IF(YDML_GCONF%YRDIMF%LUVDER) THEN
    DO JROF=KST,KEND
      P0UL(JROF,JLEV)=P0UL(JROF,JLEV)*PGM(JROF)*PGM(JROF)
      P0VL(JROF,JLEV)=P0VL(JROF,JLEV)*PGM(JROF)*PGM(JROF)
    ENDDO
  ENDIF

  IF(YDML_GCONF%YRDIMF%LVOR) THEN
    DO JROF=KST,KEND
      P0VOR(JROF,JLEV)=P0VOR(JROF,JLEV)*PGM(JROF)*PGM(JROF)
    ENDDO
  ENDIF

  IF(YDDYNA%LNHDYN) THEN
    DO JROF=KST,KEND
      P0SPDL(JROF,JLEV)=P0SPDL(JROF,JLEV)*PGM(JROF)
      P0SPDM(JROF,JLEV)=P0SPDM(JROF,JLEV)*PGM(JROF)
      P0SVDL(JROF,JLEV)=P0SVDL(JROF,JLEV)*PGM(JROF)
      P0SVDM(JROF,JLEV)=P0SVDM(JROF,JLEV)*PGM(JROF)
    ENDDO

    IF (YDDYNA%LNHXDER) THEN
      DO JROF=KST,KEND
        P0NHXL(JROF,JLEV)=P0NHXL(JROF,JLEV)*PGM(JROF)
        P0NHXM(JROF,JLEV)=P0NHXM(JROF,JLEV)*PGM(JROF)
      ENDDO
    ENDIF
  ENDIF
ENDDO

IF(.NOT.YDDYNA%LTWOTL) THEN
  DO JLEV=1,KFLEV
    DO JROF=KST,KEND
      P9DIV(JROF,JLEV)=P9DIV(JROF,JLEV)*PGM(JROF)*PGM(JROF)
      P9TL(JROF,JLEV)=P9TL(JROF,JLEV)*PGM(JROF)
      P9TM(JROF,JLEV)=P9TM(JROF,JLEV)*PGM(JROF)
    ENDDO

    IF(YDDYNA%LNHDYN) THEN
      DO JROF=KST,KEND
        P9SPDL(JROF,JLEV)=P9SPDL(JROF,JLEV)*PGM(JROF)
        P9SPDM(JROF,JLEV)=P9SPDM(JROF,JLEV)*PGM(JROF)
        P9SVDL(JROF,JLEV)=P9SVDL(JROF,JLEV)*PGM(JROF)
        P9SVDM(JROF,JLEV)=P9SVDM(JROF,JLEV)*PGM(JROF)
      ENDDO
    ENDIF
  ENDDO
ENDIF

! * GMVS:

DO JROF=KST,KEND
  P0SPL(JROF)=P0SPL(JROF)*PGM(JROF)
  P0SPM(JROF)=P0SPM(JROF)*PGM(JROF)
ENDDO

IF (.NOT.YDDYNA%LTWOTL.OR.YDDYN%NCURRENT_ITER > 0.AND.YDDYNA%LPC_FULL) THEN
  DO JROF=KST,KEND
    P9SPL(JROF)=P9SPL(JROF)*PGM(JROF)
    P9SPM(JROF)=P9SPM(JROF)*PGM(JROF)
  ENDDO
ENDIF

IF (LHOOK) CALL DR_HOOK('GPMPFC_EXPL_PART1',1,ZHOOK_HANDLE)

END SUBROUTINE GPMPFC_EXPL_PART1
