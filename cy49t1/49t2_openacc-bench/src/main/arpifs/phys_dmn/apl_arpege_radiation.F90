SUBROUTINE APL_ARPEGE_RADIATION (YDMF_PHYS_BASE_STATE, YDCFU, YDGEOMETRY, YDCPG_BNDS, YDCPG_OPTS,      &
& YDCPG_MISC, YDCPG_GPAR, YDMF_PHYS, YDMF_PHYS_SURF, YDVARS, YDMODEL, PCLCT_RAD, PALBDIR, PDECRD, PAER,&
& PAERINDS, PALBD, PALBP, PQCO2, PCEMTR, PCTRSO, PFLU_EMIS, PFLU_QSAT, PQO3, PQR, PQS, PQV, PRDG_MU0,  &
& PRDG_SOLO, PRDG_MU0LU, PRDG_MU0M, PSFSWDIF, PSFSWDIR, PSUDU, PTENT, PTRSOD, PTRSODIF, PTRSODIR)

!**** *APL_ARPEGE_RADIATION*

!     Author. 
!     ------- 
!      Philippe Marguinaud *METEO-FRANCE*
!      Original : 28-04-2021

USE YOMHOOK, ONLY : LHOOK, DR_HOOK, JPHOOK
USE PARKIND1, ONLY : JPIM, JPRB

USE MF_PHYS_BASE_STATE_TYPE_MOD &
                       , ONLY : MF_PHYS_BASE_STATE_TYPE
USE GEOMETRY_MOD       , ONLY : GEOMETRY
USE CPG_OPTS_TYPE_MOD  , ONLY : CPG_BNDS_TYPE, CPG_OPTS_TYPE
USE CPG_TYPE_MOD       , ONLY : CPG_MISC_TYPE, CPG_GPAR_TYPE
USE MF_PHYS_TYPE_MOD   , ONLY : MF_PHYS_TYPE
USE MF_PHYS_SURFACE_TYPE_MOD &
                       , ONLY : MF_PHYS_SURF_TYPE
USE FIELD_VARIABLES_MOD, ONLY : FIELD_VARIABLES
USE TYPE_MODEL         , ONLY : MODEL
USE YOMCFU             , ONLY : TCFU 


IMPLICIT NONE

TYPE (MF_PHYS_BASE_STATE_TYPE), INTENT(IN)    :: YDMF_PHYS_BASE_STATE
TYPE(TCFU),                     INTENT(IN)    :: YDCFU
TYPE(GEOMETRY),                 INTENT(IN)    :: YDGEOMETRY
TYPE(CPG_BNDS_TYPE),            INTENT(IN)    :: YDCPG_BNDS
TYPE(CPG_OPTS_TYPE),            INTENT(IN)    :: YDCPG_OPTS
TYPE(CPG_MISC_TYPE),            INTENT(INOUT) :: YDCPG_MISC
TYPE(CPG_GPAR_TYPE),            INTENT(INOUT) :: YDCPG_GPAR
TYPE(MF_PHYS_TYPE),             INTENT(INOUT) :: YDMF_PHYS
TYPE(MF_PHYS_SURF_TYPE),        INTENT(INOUT) :: YDMF_PHYS_SURF
TYPE(FIELD_VARIABLES),          INTENT(INOUT) :: YDVARS
TYPE(MODEL),                    INTENT(IN)    :: YDMODEL
REAL(KIND=JPRB),                INTENT(IN)    :: PCLCT_RAD(YDCPG_OPTS%KLON) ! total cloud cover for radiation
REAL(KIND=JPRB),                INTENT(IN)    :: PALBDIR(YDCPG_OPTS%KLON) 
REAL(KIND=JPRB),                INTENT(IN)    :: PDECRD(YDCPG_OPTS%KLON) 
REAL(KIND=JPRB),                INTENT(IN)    :: PAER(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG,6)
REAL(KIND=JPRB),                INTENT(IN)    :: PAERINDS(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB),                INTENT(IN)    :: PALBD(YDCPG_OPTS%KLON,YDCPG_OPTS%KSW)
REAL(KIND=JPRB),                INTENT(IN)    :: PALBP(YDCPG_OPTS%KLON,YDCPG_OPTS%KSW)
REAL(KIND=JPRB),                INTENT(IN)    :: PQCO2(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB),                INTENT(OUT)   :: PCEMTR(YDCPG_OPTS%KLON,0:1)
REAL(KIND=JPRB),                INTENT(OUT)   :: PCTRSO(YDCPG_OPTS%KLON,0:1)
REAL(KIND=JPRB),                INTENT(IN)    :: PFLU_EMIS (YDCPG_OPTS%KLON)
REAL(KIND=JPRB),                INTENT(IN)    :: PFLU_QSAT (YDCPG_OPTS%KLON, 1:YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB),                INTENT(IN)    :: PQO3(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB),                INTENT(IN)    :: PQR(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB),                INTENT(IN)    :: PQS(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB),                INTENT(IN)    :: PQV(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB),                INTENT(IN)    :: PRDG_MU0 (YDCPG_OPTS%KLON)
REAL(KIND=JPRB),                INTENT(IN)    :: PRDG_SOLO(YDCPG_OPTS%KLON)
REAL(KIND=JPRB),                INTENT(IN)    :: PRDG_MU0LU (YDCPG_OPTS%KLON)
REAL(KIND=JPRB),                INTENT(IN)    :: PRDG_MU0M (YDCPG_OPTS%KLON)
REAL(KIND=JPRB),                INTENT(OUT)   :: PSFSWDIF (YDCPG_OPTS%KLON,YDCPG_OPTS%KSW)
REAL(KIND=JPRB),                INTENT(OUT)   :: PSFSWDIR (YDCPG_OPTS%KLON,YDCPG_OPTS%KSW)
REAL(KIND=JPRB),                INTENT(IN)    :: PSUDU(YDCPG_OPTS%KLON)
REAL(KIND=JPRB),                INTENT(INOUT) :: PTENT(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB),                INTENT(OUT)   :: PTRSOD(YDCPG_OPTS%KLON)
REAL(KIND=JPRB),                INTENT(OUT)   :: PTRSODIF (YDCPG_OPTS%KLON,YDCPG_OPTS%KSW)
REAL(KIND=JPRB),                INTENT(OUT)   :: PTRSODIR (YDCPG_OPTS%KLON,YDCPG_OPTS%KSW)

#include "acdayd.intfb.h"
#include "acrso.intfb.h"
#include "radheat.intfb.h"
#include "recmwf.intfb.h"
#include "acraneb2.intfb.h"
#include "abor1.intfb.h"

REAL(KIND=JPRB)     :: ZCGA_DST    (YDCPG_OPTS%KLON,0,YDMODEL%YRML_PHY_RAD%YRERAD%NSW)
REAL(KIND=JPRB)     :: ZPIZA_DST   (YDCPG_OPTS%KLON,0,YDMODEL%YRML_PHY_RAD%YRERAD%NSW)
REAL(KIND=JPRB)     :: ZTAUREL_DST (YDCPG_OPTS%KLON,0,YDMODEL%YRML_PHY_RAD%YRERAD%NSW)
REAL(KIND=JPRB)     :: ZFRSODS(YDCPG_OPTS%KLON)
REAL(KIND=JPRB)     :: ZFSDNN(YDCPG_OPTS%KLON)
REAL(KIND=JPRB)     :: ZFSDNV(YDCPG_OPTS%KLON)
REAL(KIND=JPRB)     :: ZSDUR(YDCPG_OPTS%KLON)
REAL(KIND=JPRB)     :: ZDSRP(YDCPG_OPTS%KLON) 
REAL(KIND=JPRB)     :: ZALB(YDCPG_OPTS%KLON)
INTEGER (KIND=JPIM), PARAMETER :: IAERO = 12
REAL(KIND=JPRB)     :: ZAERO(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG,IAERO)
REAL(KIND=JPRB)     :: ZDUM(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB)     :: ZAEROCPL(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG,YDMODEL%YRML_PHY_RAD%YRERAD%NTLW,MERGE (6,0,YDMODEL%YRML_PHY_RAD%YREAERATM%LAERRADCPL))
LOGICAL             :: LLCALLRAD
INTEGER (KIND=JPIM) :: JLON
INTEGER (KIND=JPIM) :: JSG

REAL (KIND=JPHOOK) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK ('APL_ARPEGE_RADIATION', 0, ZHOOK_HANDLE)

ASSOCIATE (KLON => YDCPG_OPTS%KLON, KIDIA => YDCPG_BNDS%KIDIA, KFDIA => YDCPG_BNDS%KFDIA, KFLEVG => YDCPG_OPTS%KFLEVG)

IF (YDMODEL%YRML_PHY_MF%YRPHY%LRAY .AND. YDMODEL%YRML_PHY_MF%YRPHY%NRAY == 2) THEN

!$ACDC PARALLEL, TARGET=OpenMP/OpenMPSingleColumn/OpenACCSingleColumn,NAME=ACRANEB2 {
  
  CALL ACRANEB2(YDMODEL%YRCST, YDMODEL%YRML_PHY_RAD%YRERDI, YDMODEL%YRML_GCONF%YRRIP, YDMODEL%YRML_PHY_MF,            &
  & YDCPG_OPTS%LCONFX, KIDIA, KFDIA, KLON, YDMODEL%YRML_PHY_MF%YRTOPH%NTRADI, KFLEVG,                                 &
  & KLON, YDCPG_OPTS%NSTEP, YDCFU%NFRRC, YDMF_PHYS_BASE_STATE%YCPG_PHY%PREHYD,                                        &
  & YDMF_PHYS_BASE_STATE%YCPG_PHY%PREHYDF, YDMF_PHYS_BASE_STATE%YCPG_DYN%RCP%CP, YDMF_PHYS_BASE_STATE%YCPG_DYN%RCP%R, &
  & YDMF_PHYS_BASE_STATE%YCPG_PHY%XYB%DELP, YDCPG_MISC%NEB, PQV, PQCO2, YDCPG_MISC%QICE, YDCPG_MISC%QLI,              &
  & PQO3, YDMF_PHYS_BASE_STATE%T, YDMF_PHYS%OUT%ALB, PALBDIR, PFLU_EMIS, YDVARS%GEOMETRY%GELAM%T0,                    &
  & YDVARS%GEOMETRY%GEMU%T0, PRDG_MU0, PRDG_MU0LU, YDMF_PHYS_BASE_STATE%YGSP_RR%T, PDECRD,                            &
  & PCLCT_RAD, YDMF_PHYS%OPT%GDEOSI, YDMF_PHYS%OPT%GUEOSI, YDMF_PHYS%OPT%GMU0, YDMF_PHYS%OPT%GMU0_MIN,                &
  & YDMF_PHYS%OPT%GMU0_MAX, YDMF_PHYS%OPT%GDEOTI, YDMF_PHYS%OPT%GDEOTI2, YDMF_PHYS%OPT%GUEOTI,                        &
  & YDMF_PHYS%OPT%GUEOTI2, YDMF_PHYS%OPT%GEOLT, YDMF_PHYS%OPT%GEOXT, YDMF_PHYS%OPT%GRPROX, YDMF_PHYS%OPT%GMIXP,       &
  & YDMF_PHYS%OPT%GFLUXC, YDMF_PHYS%OPT%GRSURF, YDMF_PHYS_SURF%GSD_VD%PSUND, YDMF_PHYS%OUT%FRSO,                      &
  & YDMF_PHYS%OUT%FRTH, YDMF_PHYS%OUT%FRSOC, YDMF_PHYS%OUT%FRTHC, ZFRSODS, YDMF_PHYS%OUT%FRSOPS,                      &
  & YDMF_PHYS%OUT%FRSOLU, YDMF_PHYS%OUT%FRTHDS, PAER)

  ! sum downward diffuse and direct solar radiation at surface
  DO JLON = KIDIA, KFDIA
    YDMF_PHYS%OUT%FRSODS(JLON)=ZFRSODS(JLON)+YDMF_PHYS%OUT%FRSOPS(JLON)
  ENDDO

!$ACDC }

ELSEIF (YDMODEL%YRML_PHY_MF%YRPHY%LRAYFM) THEN

  LLCALLRAD=(MOD(YDCPG_OPTS%NSTEP,YDMODEL%YRML_PHY_RAD%YRERAD%NRADFR) == 0)

  IF (YDMODEL%YRML_PHY_MF%YRPHY%NCALLRAD==2) LLCALLRAD=(LLCALLRAD.AND.(YDCPG_OPTS%NSTEP<=YDMODEL%YRML_GCONF%YRRIP%NSTOP-1))
  
  ! Intermittent call to radiation scheme
  
  IF (LLCALLRAD) THEN
  
     CALL RECMWF(YDGEOMETRY%YRDIMV, YDMODEL, YDCPG_OPTS, YDCPG_BNDS, &
     & YDMODEL%YRML_PHY_RAD%YRERAD%NOZOCL, YDMODEL%YRML_PHY_RAD%YRERAD%NAERMACC, IAERO,                 &
     & PALBD, PALBP, YDMF_PHYS_BASE_STATE%YCPG_PHY%PREHYD, YDMF_PHYS_BASE_STATE%YCPG_PHY%PREHYDF,       &
     & YDCPG_MISC%NEB, PQO3, ZDUM,ZDUM,ZDUM,                                                            &
     & ZDUM,ZDUM,ZDUM,ZDUM,ZDUM,                                                                        &
     & PAER, ZAERO, ZAEROCPL, YDMF_PHYS_BASE_STATE%YCPG_PHY%XYB%DELP, PFLU_EMIS,                        &
     & PRDG_MU0M, PQV, PFLU_QSAT, YDCPG_MISC%QICE, YDCPG_MISC%QLI,                                      &
     & PQS, PQR, YDMF_PHYS_SURF%GSD_VF%PLSM, YDMF_PHYS_BASE_STATE%T, YDMF_PHYS_BASE_STATE%YGSP_RR%T,    &
     & YDVARS%RADIATION%EMTD%T0, YDVARS%RADIATION%EMTU%T0, YDVARS%RADIATION%TRSW%T0, YDMF_PHYS%OUT%FRTHC,&
     & YDMF_PHYS%OUT%FRTH, YDMF_PHYS%OUT%FRSOC, YDMF_PHYS%OUT%FRSO, PSFSWDIR, PSFSWDIF, ZFSDNN,         &
     & ZFSDNV, PCTRSO, PCEMTR, PTRSOD, PTRSODIR,                                                        &
     & PTRSODIF, ZPIZA_DST, ZCGA_DST, ZTAUREL_DST,                                                      &
     & PAERINDS, YDVARS%GEOMETRY%GELAM%T0, YDVARS%GEOMETRY%GEMU%T0, YDCPG_GPAR%SWDIR, YDCPG_GPAR%SWDIF, &
     & PRDG_MU0LU, YDMF_PHYS%OUT%ALB, YDVARS%RADIATION%RMOON%T0)

  ELSE
  
!$ACDC PARALLEL, TARGET=OpenMP/OpenMPSingleColumn/OpenACCSingleColumn,NAME=CALLRAD_F {
  
    IF (YDMODEL%YRML_PHY_MF%YRARPHY%LMSE) THEN
      DO JSG=1,YDMODEL%YRML_PHY_RAD%YRERAD%NSW
        DO JLON = KIDIA, KFDIA
          PTRSODIR(JLON,JSG)=YDCPG_GPAR%SWDIR(JLON,JSG)
          PTRSODIF(JLON,JSG)=YDCPG_GPAR%SWDIF(JLON,JSG)
        ENDDO
      ENDDO
    ENDIF
  
!$ACDC }
  
  ENDIF
  
!$ACDC PARALLEL, TARGET=OpenMP/OpenMPSingleColumn/OpenACCSingleColumn,NAME=RADHEAT {
  
  YDMF_PHYS%OUT%FRSOLU(:)=YDVARS%RADIATION%RMOON%T0(:)
  
  ! Flux update and radiative heating rates
  CALL RADHEAT (YDMODEL%YRCST,  YDMODEL%YRML_PHY_EC%YRTHF, YDMODEL%YRML_PHY_RAD%YRERAD, YDMODEL%YRML_PHY_RAD%YRERDI, YDMODEL%YRML_PHY_MF, &
  & KIDIA, KFDIA, KLON, KFLEVG, YDMF_PHYS_BASE_STATE%YCPG_PHY%PREHYD,                                                                     &
  & PFLU_EMIS, YDVARS%RADIATION%EMTD%T0, PRDG_MU0, PRDG_SOLO, PQV, PTENT, YDVARS%RADIATION%TRSW%T0, PTRSOD, YDMF_PHYS_BASE_STATE%YGSP_RR%T,&
  & YDMODEL%YRML_PHY_MF%YRPHY2%TSPHY, PTRSODIR, PTRSODIF, PALBD, PALBP, YDMF_PHYS%OUT%FRSO, YDMF_PHYS%OUT%FRTH,                           &
  & YDMF_PHYS%OUT%FRSODS, YDMF_PHYS%OUT%FRTHDS, PCEMTR, PCTRSO, YDMF_PHYS%OUT%FRSOC, YDMF_PHYS%OUT%FRTHC,                                 &
  & PSUDU, ZSDUR, ZDSRP, PSFSWDIR, PSFSWDIF, YDMF_PHYS%OUT%FRSOPS, ZFRSODS, YDMF_PHYS%OUT%FRSOPT   )
  
!$ACDC }
  
  ! Take into account day duration depending on altitude.
  IF(YDMODEL%YRML_PHY_MF%YRPHY%LDAYD) THEN
  
!$ACDC PARALLEL, TARGET=OpenMP/OpenMPSingleColumn/OpenACCSingleColumn,NAME=ACDAYD {
  
    CALL ACDAYD(YDMODEL%YRCST, YDMODEL%YRML_GCONF%YRRIP, KIDIA, KFDIA,        &
    & KLON, KFLEVG, YDCPG_OPTS%KTDIA, YDMODEL%YRML_PHY_G%YRDPHY%NTSSG,        &
    & YDVARS%GEOMETRY%GEMU%T0, PRDG_MU0, YDMF_PHYS_BASE_STATE%YCPG_DYN%PHIF,  &
    & YDMF_PHYS_BASE_STATE%YCPG_PHY%XYB%DELP, YDMF_PHYS%OUT%FRSO)
  
!$ACDC }
  
  ENDIF
  
  ! Correct solar absorption as a function of pmu0.
  
  IF(YDMODEL%YRML_PHY_MF%YRPHY0%GRSO < 1._JPRB) THEN
  
!$ACDC PARALLEL, TARGET=OpenMP/OpenMPSingleColumn/OpenACCSingleColumn,NAME=ACRSO {
  
    CALL ACRSO(YDMODEL%YRML_PHY_MF%YRPHY0, KIDIA,                                           &
    & KFDIA, KLON, KFLEVG,                                                                  &
    & YDCPG_OPTS%KTDIA, YDMODEL%YRML_PHY_G%YRDPHY%NTSSG, YDVARS%GEOMETRY%GEMU%T0,           &
    & PRDG_MU0, YDMF_PHYS_BASE_STATE%YCPG_DYN%PHIF, YDMF_PHYS_BASE_STATE%YCPG_PHY%XYB%DELP, &
    & YDMF_PHYS%OUT%FRSO)
  
  !$ACDC }
  
  ENDIF
  
  IF(.NOT.YDMODEL%YRML_PHY_MF%YRARPHY%LMSE) THEN
  
!$ACDC PARALLEL, TARGET=OpenMP/OpenMPSingleColumn/OpenACCSingleColumn,NAME=LMSE_F {
  
    DO JLON = KIDIA, KFDIA
      ZALB(JLON)=0.0_JPRB
      DO JSG=1,YDMODEL%YRML_PHY_RAD%YRERAD%NSW
        ZALB(JLON)=ZALB(JLON)+0.5_JPRB*(PALBD(JLON,JSG)+PALBP(JLON,JSG))
      ENDDO
      ZALB(JLON)=ZALB(JLON)/REAL(YDMODEL%YRML_PHY_RAD%YRERAD%NSW, JPRB)
      YDMF_PHYS%OUT%FRSODS(JLON)=YDMF_PHYS%OUT%FRSO(JLON,KFLEVG,1)/(1.0_JPRB-ZALB(JLON))
    ENDDO
  
  !$ACDC }
  
  ENDIF
  
  
  ! Compute Sunshine Duration (in seconds)
  
  IF (.NOT.YDCPG_OPTS%LCONFX) THEN
  
!$ACDC PARALLEL, TARGET=OpenMP/OpenMPSingleColumn/OpenACCSingleColumn,NAME=SUNSHINE {
  
  !DEC$ IVDEP
    DO JLON = KIDIA, KFDIA
      IF(YDMF_PHYS%OUT%FRSODS(JLON) >= YDMODEL%YRML_PHY_RAD%YRERDI%RSUNDUR) THEN
        YDMF_PHYS_SURF%GSD_VD%PSUND(JLON)=YDMF_PHYS_SURF%GSD_VD%PSUND(JLON)+1.0_JPRB*YDMODEL%YRML_GCONF%YRRIP%TSTEP
      ENDIF
    ENDDO
  
!$ACDC }
  
  ENDIF

ELSE
  CALL ABOR1 ('APL_ARPEGE_RADIATION: WRONG SETTINGS')
ENDIF

! Direct normal irradiance with securities

!$ACDC PARALLEL, TARGET=OpenMP/OpenMPSingleColumn/OpenACCSingleColumn,NAME=DIRECT {

DO JLON = KIDIA, KFDIA
  YDMF_PHYS%OUT%FRSDNI(JLON)=YDMF_PHYS%OUT%FRSOPS(JLON)
  IF (PRDG_MU0(JLON) > 3.0E-02_JPRB) THEN
    YDMF_PHYS%OUT%FRSDNI(JLON)=YDMF_PHYS%OUT%FRSOPS(JLON)/PRDG_MU0(JLON)
  ENDIF
  YDMF_PHYS%OUT%FRSDNI(JLON)=MAX(0.0_JPRB,YDMF_PHYS%OUT%FRSDNI(JLON))
ENDDO
  
! Global normal irradiance

DO JLON = KIDIA, KFDIA
  YDMF_PHYS%OUT%FRSGNI(JLON)=YDMF_PHYS%OUT%FRSDNI(JLON)+0.5_JPRB*(                     &
  & (1.0_JPRB+PRDG_MU0(JLON))*(YDMF_PHYS%OUT%FRSODS(JLON)-YDMF_PHYS%OUT%FRSOPS(JLON))+ &
  & (1.0_JPRB-PRDG_MU0(JLON))*(YDMF_PHYS%OUT%FRSODS(JLON)-YDMF_PHYS%OUT%FRSO  (JLON,KFLEVG,1)))
ENDDO

!$ACDC }

END ASSOCIATE

IF (LHOOK) CALL DR_HOOK ('APL_ARPEGE_RADIATION', 1, ZHOOK_HANDLE)

END SUBROUTINE APL_ARPEGE_RADIATION

