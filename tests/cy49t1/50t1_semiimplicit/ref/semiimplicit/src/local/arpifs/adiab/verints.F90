SUBROUTINE VERINTS(KPROMA,KST,KEND,KLEVIN,KLEVOUT,PINTE,PIN,POUTS)

!$ACDC singleblock

! Purpose:
!  compute the full vertical integral in VFE scheme
!
! Input:
!  KPROMA: horizontal dimension
!  KST/KEND: starting/ending points to loop over
!  KLEVIN: "VFE coefficients" dimension of VFE array and input array (generally NFLEVG+2)
!  KLEVOUT: 1st dimension of VFE array (ie NFLEVG+1)
!  PINTE: VFE array
!  PIN: input array
!
! Output:
!  POUTS: full VFE integral (corresponds to index KLEVOUT of PINTE)
!
! Author:
!  H Petithomme, Sep 2023
!
!     Modifications.
!     --------------
!      R. El Khatib 03-Mar-2025 Do not convert uninitialized variables
!     ------------------------------------------------------------------

USE PARKIND1, ONLY : JPIM, JPRB, JPRD
USE YOMHOOK , ONLY : LHOOK, DR_HOOK, JPHOOK
USE OML_MOD   , ONLY : OML_IN_PARALLEL
#ifdef FXTRAN_ACDC
USE FXTRAN_ACDC_GEMV_MOD, ONLY : FXTRAN_ACDC_GEMV
#endif

IMPLICIT NONE

INTEGER(KIND=JPIM) ,INTENT(IN)           :: KPROMA,KLEVIN,KLEVOUT,KST,KEND
REAL(KIND=JPRD)    ,INTENT(IN)           :: PINTE(KLEVOUT,KLEVIN)
REAL(KIND=JPRB)    ,INTENT(IN)  ,TARGET  :: PIN(KPROMA,KLEVIN)
REAL(KIND=JPRD)    ,INTENT(OUT)          :: POUTS(KPROMA)

LOGICAL, PARAMETER :: LLDOUBLE = JPRB == JPRD

LOGICAL, PARAMETER :: LLVERINT_ON_CPU = .TRUE.

LOGICAL :: LLOML_IN_PARALLEL
LOGICAL :: LLFXTRAN_ACDC_GEMM

REAL(KIND=JPRD), POINTER :: ZIN (:,:)
REAL(KIND=JPRD), TARGET  :: ZIN0(KPROMA, MERGE (KLEVIN, 0, .NOT. LLDOUBLE))

INTEGER (KIND=JPIM) :: JROF, JLEVIN

REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('VERINTS',0,ZHOOK_HANDLE)

IF (LLVERINT_ON_CPU) THEN
  LLOML_IN_PARALLEL = OML_IN_PARALLEL ()
ELSE
  LLOML_IN_PARALLEL = .TRUE.
ENDIF

IF (LLDOUBLE) THEN
#ifndef PARKIND1_SINGLE
  ZIN => PIN
#endif
ELSE
  ZIN => ZIN0

!$OMP PARALLEL DO SCHEDULE(STATIC) PRIVATE(JLEVIN) IF (.NOT. LLOML_IN_PARALLEL)
  DO JLEVIN = 1, KLEVIN
    ZIN(KST:KEND,JLEVIN) = REAL(PIN(KST:KEND,JLEVIN),JPRD)
  ENDDO
!$OMP END PARALLEL DO

ENDIF

LLFXTRAN_ACDC_GEMM = .FALSE.
#ifdef FXTRAN_ACDC
CALL FXTRAN_ACDC_GEMV (KST, KEND, 'N', 'T', KPROMA, 1, KLEVIN, 1.0_JPRD, ZIN, &
                     & KPROMA, PINTE, KLEVOUT, 0.0_JPRD, POUTS, KPROMA, LLFXTRAN_ACDC_GEMM)
#endif

IF (.NOT. LLFXTRAN_ACDC_GEMM) THEN

!$ACDC ABORT {

  CALL DGEMM('N','T',KEND-KST+1,1,KLEVIN,1.0_JPRD,ZIN,KPROMA,PINTE(KLEVOUT,1),KLEVOUT,0.0_JPRD,POUTS,KPROMA)

!$ACDC }

ENDIF

IF (LHOOK) CALL DR_HOOK('VERINTS',1,ZHOOK_HANDLE)

END SUBROUTINE VERINTS


