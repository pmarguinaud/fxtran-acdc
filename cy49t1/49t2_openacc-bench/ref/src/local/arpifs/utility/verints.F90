SUBROUTINE VERINTS(KPROMA,KST,KEND,KLEVIN,KLEVOUT,PINTE,PIN,POUTS)

!$ACDC singlecolumn --set-variables LLVERINT_ON_CPU=.FALSE.,LLSIMPLE_DGEMM=.TRUE. --no-check-pointers-dims ZIN,ZOUT  --process-pointers

! Purpose:
!  compute the full vertical integral in VFE scheme
!
! Input:
!  KPROMA: horizontal dimension
!  KST/KEND: starting/ending points to loop over
!  KLEVIN: "VFE coefficients" dimension of VFE array and input array (generally NFLEVG+2)
!  KLEVOUT: 1st dimension of VFE array (ie NFLEVG+1)
!  PINTE: VFE array
!  PIN: input array
!
! Output:
!  POUTS: full VFE integral (corresponds to index KLEVOUT of PINTE)
!
! Author:
!  H Petithomme, Sep 2023

USE PARKIND1, ONLY : JPIM, JPRB, JPRD
USE YOMHOOK , ONLY : LHOOK, DR_HOOK, JPHOOK

IMPLICIT NONE

INTEGER(KIND=JPIM) ,INTENT(IN)           :: KPROMA,KLEVIN,KLEVOUT,KST,KEND
REAL(KIND=JPRD)    ,INTENT(IN)           :: PINTE(KLEVOUT,KLEVIN)
REAL(KIND=JPRB)    ,INTENT(IN)  ,TARGET  :: PIN(KPROMA,KLEVIN)
REAL(KIND=JPRD)    ,INTENT(OUT)          :: POUTS(KPROMA)

LOGICAL, PARAMETER :: LLDOUBLE = JPRB == JPRD

LOGICAL, SAVE :: LLSIMPLE_DGEMM = .FALSE.
LOGICAL, PARAMETER :: LLVERINT_ON_CPU = .TRUE.
CHARACTER(LEN=8), SAVE :: CLENV = ''

REAL(KIND=JPRD), POINTER :: ZIN (:,:)
REAL(KIND=JPRD), TARGET  :: ZIN0(KPROMA, MERGE (KLEVIN, 0, .NOT. LLDOUBLE))

INTEGER (KIND=JPIM) :: JROF, JLEVIN

REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('VERINTS',0,ZHOOK_HANDLE)

IF (LLVERINT_ON_CPU) THEN
  IF (CLENV == '') THEN
    CALL GETENV ('LLSIMPLE_DGEMM', CLENV)
    LLSIMPLE_DGEMM = CLENV == '1'
  ENDIF
  CLENV = "done"
ENDIF

IF (LLDOUBLE) THEN
#ifndef PARKIND1_SINGLE
  ZIN => PIN
#endif
ELSE
  ZIN => ZIN0
  ZIN(:,:) = REAL(PIN(:,:),JPRD)
ENDIF

IF (LLSIMPLE_DGEMM) THEN
  DO JROF = KST, KEND
    POUTS (JROF) = 0._JPRD
  ENDDO
  DO JLEVIN = 1, KLEVIN
    DO JROF = KST, KEND
      POUTS (JROF) = POUTS (JROF) + ZIN (JROF, JLEVIN) * PINTE (KLEVOUT, JLEVIN) 
    ENDDO
  ENDDO
ELSE
  CALL DGEMM('N','T',KEND,1,KLEVIN,1.0_JPRD,ZIN,KPROMA,PINTE(KLEVOUT,1),KLEVOUT,0.0_JPRD,POUTS,KPROMA)
ENDIF

IF (LHOOK) CALL DR_HOOK('VERINTS',1,ZHOOK_HANDLE)

END SUBROUTINE VERINTS


