SUBROUTINE APL_ARPEGE_ALBEDO_COMPUTATION (YDMF_PHYS_BASE_STATE, YDCPG_BNDS, YDCPG_OPTS, YDMF_PHYS, &
& YDMF_PHYS_SURF, YDMODEL, PALBD, PALBP, PEPS0, PFLU_EMIS, PFLU_NEIJ, PRDG_MU0, PALBDIR)

!**** *APL_ARPEGE_ALBEDO_COMPUTATION*  

!     Author. 
!     ------- 
!      Philippe Marguinaud *METEO-FRANCE*
!      Original : 28-04-2021

USE YOMHOOK, ONLY : LHOOK, DR_HOOK, JPHOOK
USE PARKIND1, ONLY : JPIM, JPRB

USE MF_PHYS_BASE_STATE_TYPE_MOD &
                       , ONLY : MF_PHYS_BASE_STATE_TYPE
USE CPG_OPTS_TYPE_MOD  , ONLY : CPG_BNDS_TYPE, CPG_OPTS_TYPE
USE MF_PHYS_TYPE_MOD   , ONLY : MF_PHYS_TYPE
USE MF_PHYS_SURFACE_TYPE_MOD & 
                       , ONLY : MF_PHYS_SURF_TYPE
USE TYPE_MODEL         , ONLY : MODEL


IMPLICIT NONE

TYPE(MF_PHYS_BASE_STATE_TYPE),  INTENT(IN)    :: YDMF_PHYS_BASE_STATE
TYPE(CPG_BNDS_TYPE),            INTENT(IN)    :: YDCPG_BNDS
TYPE(CPG_OPTS_TYPE),            INTENT(IN)    :: YDCPG_OPTS
TYPE(MF_PHYS_TYPE),             INTENT(INOUT) :: YDMF_PHYS
TYPE(MF_PHYS_SURF_TYPE),        INTENT(INOUT) :: YDMF_PHYS_SURF
TYPE(MODEL),                    INTENT(IN)    :: YDMODEL
REAL(KIND=JPRB),                INTENT(OUT)   :: PALBD(YDCPG_OPTS%KLON,YDCPG_OPTS%KSW)
REAL(KIND=JPRB),                INTENT(OUT)   :: PALBP(YDCPG_OPTS%KLON,YDCPG_OPTS%KSW)
REAL(KIND=JPRB),                INTENT(IN)    :: PEPS0
REAL(KIND=JPRB),                INTENT(OUT)   :: PFLU_EMIS (YDCPG_OPTS%KLON)
REAL(KIND=JPRB),                INTENT(IN)    :: PFLU_NEIJ (YDCPG_OPTS%KLON)
REAL(KIND=JPRB),                INTENT(IN)    :: PRDG_MU0 (YDCPG_OPTS%KLON)
REAL(KIND=JPRB),                INTENT(OUT)   :: PALBDIR (YDCPG_OPTS%KLON)


REAL(KIND=JPRB)     :: ZMERL(YDCPG_OPTS%KLON)
REAL(KIND=JPRB)     :: ZLAMB
INTEGER (KIND=JPIM) :: JLON
INTEGER (KIND=JPIM) :: JSG

REAL (KIND=JPHOOK) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK ('APL_ARPEGE_ALBEDO_COMPUTATION', 0, ZHOOK_HANDLE)

ASSOCIATE (KLON => YDCPG_OPTS%KLON, KIDIA => YDCPG_BNDS%KIDIA, KFDIA => YDCPG_BNDS%KFDIA, KFLEVG => YDCPG_OPTS%KFLEVG)

IF (.NOT.YDMODEL%YRML_PHY_MF%YRARPHY%LMSE) THEN

!$ACDC PARALLEL, TARGET=OpenMP/OpenMPSingleColumn/OpenACCSingleColumn {

!DEC$ IVDEP
  DO JLON = KIDIA, KFDIA
    YDMF_PHYS%OUT%ALB(JLON)=YDMF_PHYS_SURF%GSD_VF%PALBF(JLON)-PFLU_NEIJ(JLON)*(YDMF_PHYS_SURF%GSD_VF%PALBF(JLON)&
    & -MAX(YDMF_PHYS_SURF%GSD_VF%PALBF(JLON),YDMODEL%YRML_PHY_MF%YRPHY1%ALCRIN))
    PFLU_EMIS(JLON)=YDMF_PHYS_SURF%GSD_VF%PEMISF(JLON)-PFLU_NEIJ(JLON)*(YDMF_PHYS_SURF%GSD_VF%PEMISF(JLON)-YDMODEL%YRML_PHY_MF%YRPHY1%EMCRIN)
  ENDDO
      
  IF (YDMODEL%YRML_PHY_MF%YRPHY%LRAYFM) THEN
  
  ! diffuse and direct (parallel) albedo in NSW solar intervals
        
!DEC$ IVDEP
    DO JLON = KIDIA, KFDIA
      ZMERL(JLON)=(1.0_JPRB-YDMF_PHYS_SURF%GSD_VF%PLSM(JLON))*(1.0_JPRB&
      & -MAX(0.0_JPRB,SIGN(1.0_JPRB,YDMODEL%YRML_PHY_MF%YRPHY1%TMERGL-YDMF_PHYS_BASE_STATE%YGSP_RR%T(JLON))))
      PFLU_EMIS(JLON)=PFLU_EMIS(JLON)*YDMF_PHYS_SURF%GSD_VF%PLSM(JLON)+ZMERL(JLON)*YDMODEL%YRML_PHY_MF%YRPHY1%EMMMER&
      & +(1.0_JPRB-YDMF_PHYS_SURF%GSD_VF%PLSM(JLON))*(1.0_JPRB-ZMERL(JLON))*YDMODEL%YRML_PHY_MF%YRPHY1%EMMGLA
    ENDDO
    DO JSG=1,YDMODEL%YRML_PHY_RAD%YRERAD%NSW
      DO JLON = KIDIA, KFDIA
        PALBD(JLON,JSG)=(1.0_JPRB-ZMERL(JLON))*YDMF_PHYS%OUT%ALB(JLON)+&
        & ZMERL(JLON) *YDMODEL%YRML_PHY_MF%YRPHY1%ALBMED
        PALBP(JLON,JSG)=(1.0_JPRB-ZMERL(JLON))*YDMF_PHYS%OUT%ALB(JLON)+&
        & ZMERL(JLON) *&
        & MAX(0.037_JPRB/(1.1_JPRB*PRDG_MU0(JLON)**1.4_JPRB+0.15_JPRB),PEPS0)
      ENDDO
    ENDDO
  
  ELSEIF (YDMODEL%YRML_PHY_MF%YRPHY%LRAY) THEN

    ! direct (parallel) albedo for ACRANEB/ACRANEB2, Geleyn's formula
    ! with given proportion of Lambertian scattering
    DO JLON = KIDIA, KFDIA
      IF (YDMF_PHYS_SURF%GSD_VF%PLSM(JLON) < 0.5_JPRB .AND. YDMF_PHYS_BASE_STATE%YGSP_RR%T(JLON) >= YDMODEL%YRML_PHY_MF%YRPHY1%TMERGL) THEN
        ZLAMB = YDMODEL%YRML_PHY_MF%YRPHY3%RLAMB_WATER  ! water surface (open sea)
      ELSE
        ZLAMB = YDMODEL%YRML_PHY_MF%YRPHY3%RLAMB_SOLID  ! solid surface (frozen sea or land)
      ENDIF
      PALBDIR(JLON)=ZLAMB*YDMF_PHYS%OUT%ALB(JLON)+(1._JPRB-ZLAMB)*(1._JPRB+&
       & 0.5_JPRB*PRDG_MU0(JLON)*(1.0_JPRB/YDMF_PHYS%OUT%ALB(JLON)-1.0_JPRB))/   (&
       & 1.0_JPRB+PRDG_MU0(JLON)*(1.0_JPRB/YDMF_PHYS%OUT%ALB(JLON)-1.0_JPRB))**2
    ENDDO

  ENDIF

!$ACDC }

ENDIF  

END ASSOCIATE

IF (LHOOK) CALL DR_HOOK ('APL_ARPEGE_ALBEDO_COMPUTATION', 1, ZHOOK_HANDLE)

END SUBROUTINE APL_ARPEGE_ALBEDO_COMPUTATION

