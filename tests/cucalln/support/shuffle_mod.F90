MODULE SHUFFLE_MOD

USE PARKIND1, ONLY : JPIM, JPRB, JPRD

IMPLICIT NONE


PRIVATE
PUBLIC :: DD12

TYPE DD12
  INTEGER :: IPROMA1, IPROMA2
  INTEGER :: IGPBLKS1, IGPBLKS2
  INTEGER :: IGPTOT1, IGPTOT2
  INTEGER, ALLOCATABLE :: JLON1 (:,:)
  INTEGER, ALLOCATABLE :: JBLK1 (:,:)
CONTAINS
  PROCEDURE :: INIT => DD12_INIT
  GENERIC :: SHUFFLE => SHUFFLE2K, SHUFFLE2L
  PROCEDURE :: SHUFFLE2K, SHUFFLE2L 
  GENERIC :: SHUFFLE => SHUFFLE2P_DB, SHUFFLE3P_DB, SHUFFLE4P_DB
  PROCEDURE :: SHUFFLE2P_DB
  PROCEDURE :: SHUFFLE3P_DB
  PROCEDURE :: SHUFFLE4P_DB
#ifdef PARKIND1_SINGLE
  GENERIC :: SHUFFLE => SHUFFLE2P_BD, SHUFFLE3P_BD, SHUFFLE4P_BD
  PROCEDURE :: SHUFFLE2P_BD
  PROCEDURE :: SHUFFLE3P_BD
  PROCEDURE :: SHUFFLE4P_BD
#endif
END TYPE

CONTAINS

SUBROUTINE DD12_INIT (SELF, KDIMS1, KDIMS2)

CLASS (DD12) :: SELF
INTEGER, INTENT (IN) :: KDIMS1 (:)
INTEGER, INTENT (IN) :: KDIMS2 (:)

INTEGER :: JBLK1, JBLK2
INTEGER :: JLON1, JLON2
INTEGER :: JGLO1, JGLO2

INTEGER :: IRANK

IF (SIZE (KDIMS1) /= SIZE (KDIMS2)) STOP 1

IRANK = SIZE (KDIMS1)

SELF%IPROMA1 = KDIMS1 (1); SELF%IGPBLKS1 = KDIMS1 (IRANK)
SELF%IPROMA2 = KDIMS2 (1); SELF%IGPBLKS2 = KDIMS2 (IRANK)

SELF%IGPTOT1 = SELF%IPROMA1 * SELF%IGPBLKS1
SELF%IGPTOT2 = SELF%IPROMA2 * SELF%IGPBLKS2

ALLOCATE (SELF%JLON1 (SELF%IPROMA2, SELF%IGPBLKS2))
ALLOCATE (SELF%JBLK1 (SELF%IPROMA2, SELF%IGPBLKS2))

DO JBLK2 = 1, SELF%IGPBLKS2
  DO JLON2 = 1, SELF%IPROMA2
    JGLO2 = (JBLK2 - 1) * SELF%IPROMA2 + JLON2
    JGLO1 = 1 + MODULO (JGLO2-1, SELF%IGPTOT1)
    JBLK1 = (JGLO1+SELF%IPROMA1-1) / SELF%IPROMA1
    JLON1 = 1 + MODULO (JGLO1-1, SELF%IPROMA1)
    SELF%JLON1 (JLON2, JBLK2) = JLON1
    SELF%JBLK1 (JLON2, JBLK2) = JBLK1
  ENDDO
ENDDO

END SUBROUTINE

SUBROUTINE SHUFFLE2K (SELF, K1, K2)

CLASS (DD12),        INTENT (IN)  :: SELF
INTEGER (KIND=JPIM), INTENT (IN)  :: K1 (:,:) 
INTEGER (KIND=JPIM), INTENT (OUT) :: K2 (:,:)

INTEGER :: JBLK2, JLON2

DO JBLK2 = 1, SELF%IGPBLKS2
  DO JLON2 = 1, SELF%IPROMA2
    K2 (JLON2, JBLK2) = K1 (SELF%JLON1 (JLON2, JBLK2), SELF%JBLK1 (JLON2, JBLK2))
  ENDDO
ENDDO

END SUBROUTINE

SUBROUTINE SHUFFLE2L (SELF, LD1, LD2)

CLASS (DD12),      INTENT (IN)  :: SELF
LOGICAL,           INTENT (IN)  :: LD1 (:,:) 
LOGICAL,           INTENT (OUT) :: LD2 (:,:)

INTEGER :: JBLK2, JLON2

DO JBLK2 = 1, SELF%IGPBLKS2
  DO JLON2 = 1, SELF%IPROMA2
    LD2 (JLON2, JBLK2) = LD1 (SELF%JLON1 (JLON2, JBLK2), SELF%JBLK1 (JLON2, JBLK2))
  ENDDO
ENDDO

END SUBROUTINE

SUBROUTINE SHUFFLE2P_DB (SELF, P1, P2)

CLASS (DD12),      INTENT (IN)  :: SELF
REAL (KIND=JPRD),  INTENT (IN)  :: P1 (:,:) 
REAL (KIND=JPRB),  INTENT (OUT) :: P2 (:,:)

INTEGER :: JBLK2, JLON2

DO JBLK2 = 1, SELF%IGPBLKS2
  DO JLON2 = 1, SELF%IPROMA2
    P2 (JLON2, JBLK2) = P1 (SELF%JLON1 (JLON2, JBLK2), SELF%JBLK1 (JLON2, JBLK2))
  ENDDO
ENDDO

END SUBROUTINE

SUBROUTINE SHUFFLE3P_DB (SELF, P1, P2)

CLASS (DD12),      INTENT (IN)  :: SELF
REAL (KIND=JPRD),  INTENT (IN)  :: P1 (:,:,:) 
REAL (KIND=JPRB),  INTENT (OUT) :: P2 (:,:,:)

INTEGER :: JBLK2, JLON2

DO JBLK2 = 1, SELF%IGPBLKS2
  DO JLON2 = 1, SELF%IPROMA2
    P2 (JLON2, :, JBLK2) = P1 (SELF%JLON1 (JLON2, JBLK2), :, SELF%JBLK1 (JLON2, JBLK2))
  ENDDO
ENDDO

END SUBROUTINE

SUBROUTINE SHUFFLE4P_DB (SELF, P1, P2)

CLASS (DD12),      INTENT (IN)  :: SELF
REAL (KIND=JPRD),  INTENT (IN)  :: P1 (:,:,:,:) 
REAL (KIND=JPRB),  INTENT (OUT) :: P2 (:,:,:,:)

INTEGER :: JBLK2, JLON2

DO JBLK2 = 1, SELF%IGPBLKS2
  DO JLON2 = 1, SELF%IPROMA2
    P2 (JLON2, :, :, JBLK2) = P1 (SELF%JLON1 (JLON2, JBLK2), :, :, SELF%JBLK1 (JLON2, JBLK2))
  ENDDO
ENDDO

END SUBROUTINE

#ifdef PARKIND1_SINGLE

SUBROUTINE SHUFFLE2P_BD (SELF, P1, P2)

CLASS (DD12),      INTENT (IN)  :: SELF
REAL (KIND=JPRB),  INTENT (IN)  :: P1 (:,:) 
REAL (KIND=JPRD),  INTENT (OUT) :: P2 (:,:)

INTEGER :: JBLK2, JLON2

DO JBLK2 = 1, SELF%IGPBLKS2
  DO JLON2 = 1, SELF%IPROMA2
    P2 (JLON2, JBLK2) = P1 (SELF%JLON1 (JLON2, JBLK2), SELF%JBLK1 (JLON2, JBLK2))
  ENDDO
ENDDO

END SUBROUTINE

SUBROUTINE SHUFFLE3P_BD (SELF, P1, P2)

CLASS (DD12),      INTENT (IN)  :: SELF
REAL (KIND=JPRB),  INTENT (IN)  :: P1 (:,:,:) 
REAL (KIND=JPRD),  INTENT (OUT) :: P2 (:,:,:)

INTEGER :: JBLK2, JLON2

DO JBLK2 = 1, SELF%IGPBLKS2
  DO JLON2 = 1, SELF%IPROMA2
    P2 (JLON2, :, JBLK2) = P1 (SELF%JLON1 (JLON2, JBLK2), :, SELF%JBLK1 (JLON2, JBLK2))
  ENDDO
ENDDO

END SUBROUTINE

SUBROUTINE SHUFFLE4P_BD (SELF, P1, P2)

CLASS (DD12),      INTENT (IN)  :: SELF
REAL (KIND=JPRB),  INTENT (IN)  :: P1 (:,:,:,:) 
REAL (KIND=JPRD),  INTENT (OUT) :: P2 (:,:,:,:)

INTEGER :: JBLK2, JLON2

DO JBLK2 = 1, SELF%IGPBLKS2
  DO JLON2 = 1, SELF%IPROMA2
    P2 (JLON2, :, :, JBLK2) = P1 (SELF%JLON1 (JLON2, JBLK2), :, :, SELF%JBLK1 (JLON2, JBLK2))
  ENDDO
ENDDO

END SUBROUTINE

#endif

END MODULE
