SUBROUTINE APL_ARPEGE_INIT_SURFEX (YDMF_PHYS_BASE_STATE, YDGEOMETRY, YDCPG_BNDS, YDCPG_OPTS,      &
& YDCPG_MISC, YDCPG_GPAR, YDMF_PHYS, YDVARS, YDMODEL, PALBD, PALBP, PFLU_EMIS, PSGROUPEL, PSRAIN, &
& PSSNOW, PTSN)

!**** *APL_ARPEGE_INIT_SURFEX*  

!     Author. 
!     ------- 
!      Philippe Marguinaud *METEO-FRANCE*
!      Original : 28-04-2021

USE YOMHOOK, ONLY : LHOOK, DR_HOOK, JPHOOK
USE PARKIND1, ONLY : JPIM, JPRB

USE MF_PHYS_BASE_STATE_TYPE_MOD &
                       , ONLY : MF_PHYS_BASE_STATE_TYPE
USE GEOMETRY_MOD       , ONLY : GEOMETRY
USE CPG_OPTS_TYPE_MOD   , ONLY : CPG_BNDS_TYPE, CPG_OPTS_TYPE
USE CPG_TYPE_MOD       , ONLY : CPG_MISC_TYPE, CPG_GPAR_TYPE
USE MF_PHYS_TYPE_MOD   , ONLY : MF_PHYS_TYPE
USE FIELD_VARIABLES_MOD, ONLY : FIELD_VARIABLES
USE TYPE_MODEL         , ONLY : MODEL


IMPLICIT NONE

TYPE (MF_PHYS_BASE_STATE_TYPE), INTENT(IN)    :: YDMF_PHYS_BASE_STATE
TYPE(GEOMETRY),                 INTENT(IN)    :: YDGEOMETRY
TYPE(CPG_BNDS_TYPE),            INTENT(IN)    :: YDCPG_BNDS
TYPE(CPG_OPTS_TYPE),            INTENT(IN)    :: YDCPG_OPTS
TYPE(CPG_MISC_TYPE),            INTENT(INOUT) :: YDCPG_MISC
TYPE(CPG_GPAR_TYPE),            INTENT(INOUT) :: YDCPG_GPAR
TYPE(MF_PHYS_TYPE),             INTENT(INOUT) :: YDMF_PHYS
TYPE(FIELD_VARIABLES),          INTENT(INOUT) :: YDVARS
TYPE(MODEL),                    INTENT(IN)    :: YDMODEL
REAL(KIND=JPRB),                INTENT(OUT)   :: PALBD(YDCPG_OPTS%KLON,YDCPG_OPTS%KSW)
REAL(KIND=JPRB),                INTENT(OUT)   :: PALBP(YDCPG_OPTS%KLON,YDCPG_OPTS%KSW)
REAL(KIND=JPRB),                INTENT(OUT)   :: PFLU_EMIS (YDCPG_OPTS%KLON)
REAL(KIND=JPRB),                INTENT(OUT)   :: PSGROUPEL(YDCPG_OPTS%KLON)
REAL(KIND=JPRB),                INTENT(OUT)   :: PSRAIN(YDCPG_OPTS%KLON)
REAL(KIND=JPRB),                INTENT(OUT)   :: PSSNOW(YDCPG_OPTS%KLON)
REAL(KIND=JPRB),                INTENT(OUT)   :: PTSN(YDCPG_OPTS%KLON)

#include "abor1.intfb.h"
#include "aro_ground_diag_z0.h"

INTEGER (KIND=JPIM) :: JLON
INTEGER (KIND=JPIM) :: JSG

REAL (KIND=JPHOOK) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK ('APL_ARPEGE_INIT_SURFEX', 0, ZHOOK_HANDLE)

ASSOCIATE (KLON => YDCPG_OPTS%KLON, KIDIA => YDCPG_BNDS%KIDIA, KFDIA => YDCPG_BNDS%KFDIA, KFLEVG => YDCPG_OPTS%KFLEVG)

! INITIALISATIONS LIEES AU SCHEMA DE SURFACE EXTERNALISE

IF (YDMODEL%YRML_PHY_MF%YRARPHY%LMSE) THEN

! INITIALISATION DU SCHEMA DE SURFACE EXTERNALISE ET DES VARIABLES PSEUDO-HISTORIQUES ASSOCIEES

!$ACDC SERIAL {

  IF (YDMODEL%YRML_PHY_MF%YRPARAR%NSWB_MNH /= YDMODEL%YRML_PHY_RAD%YRERAD%NSW) THEN
    CALL ABOR1 ('APL_ARPEGE_INIT_SURFEX: NSWB_MNH /= NSW')
  ENDIF

!$ACDC }

!$ACDC PARALLEL {


  DO JLON = KIDIA, KFDIA
    YDMF_PHYS%OUT%GZ0 (JLON) = YDCPG_GPAR%GZ0(JLON)
    YDMF_PHYS%OUT%GZ0H (JLON) = YDCPG_GPAR%GZ0H(JLON)
    PFLU_EMIS (JLON) = YDCPG_GPAR%VEMIS(JLON)
    YDCPG_MISC%QS (JLON) = YDCPG_GPAR%VQS(JLON)
    YDMF_PHYS_BASE_STATE%YGSP_RR%T (JLON) = YDCPG_GPAR%VTS(JLON)
    PSRAIN (JLON) = YDCPG_GPAR%RAIN(JLON)
    PSSNOW (JLON) = YDCPG_GPAR%SNOW(JLON)
    PSGROUPEL(JLON) = 0._JPRB
    PTSN (JLON) = YDMF_PHYS_BASE_STATE%YGSP_RR%T(JLON)
  ENDDO

!$ACDC }


!$ACDC PARALLEL {
    
! FMR  radiation => NSW solar bands
  DO JSG=1,YDMODEL%YRML_PHY_RAD%YRERAD%NSW
    DO JLON = KIDIA, KFDIA
      PALBP(JLON,JSG) = YDCPG_GPAR%ALBDIR(JLON,JSG)
      PALBD(JLON,JSG) = YDCPG_GPAR%ALBSCA(JLON,JSG)
    ENDDO
  ENDDO
  DO JLON = KIDIA, KFDIA
    YDMF_PHYS%OUT%ALB(JLON)=0.0_JPRB
  ENDDO
  DO JSG=1,YDMODEL%YRML_PHY_RAD%YRERAD%NSW
    DO JLON = KIDIA, KFDIA
      YDMF_PHYS%OUT%ALB(JLON)=YDMF_PHYS%OUT%ALB(JLON)+0.5*(PALBP(JLON,JSG)+PALBD(JLON,JSG))
    ENDDO
  ENDDO
  DO JLON = KIDIA, KFDIA
    YDMF_PHYS%OUT%ALB(JLON)=YDMF_PHYS%OUT%ALB(JLON)/REAL(YDMODEL%YRML_PHY_RAD%YRERAD%NSW,JPRB)
  ENDDO
    

  DO JLON = KIDIA, KFDIA
    IF (PFLU_EMIS(JLON)==0._JPRB) THEN
      PFLU_EMIS(JLON) = 0.99_JPRB
      YDMF_PHYS_BASE_STATE%YGSP_RR%T  (JLON) = 288.0_JPRB
      YDMF_PHYS%OUT%ALB (JLON) = 0.1_JPRB
    ENDIF
  ENDDO

!$ACDC }

! Define z0;z0h if it's necessary

  IF (YDCPG_OPTS%LCONFX) THEN

!$ACDC PARALLEL {

    CALL ARO_GROUND_DIAG_Z0 (YDCPG_BNDS%KBL, KFDIA-KIDIA+1, KIDIA, KFDIA,                                   &
    & YDGEOMETRY%YRDIM%NDGUNG, YDGEOMETRY%YRDIM%NDGUXG, YDGEOMETRY%YRDIM%NDLUNG, YDGEOMETRY%YRDIM%NDLUXG,   &
    & YDVARS%GEOMETRY%RINDX%T0(KIDIA:KFDIA), YDVARS%GEOMETRY%RINDY%T0(KIDIA:KFDIA),                         &
    & YDMF_PHYS%OUT%GZ0(KIDIA:KFDIA), YDMF_PHYS%OUT%GZ0H(KIDIA:KFDIA))

!$ACDC }

  ENDIF

ENDIF

END ASSOCIATE

IF (LHOOK) CALL DR_HOOK ('APL_ARPEGE_INIT_SURFEX', 1, ZHOOK_HANDLE)

END SUBROUTINE APL_ARPEGE_INIT_SURFEX

