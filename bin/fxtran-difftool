#!/usr/bin/env perl

=head1 NAME

fxtran-difftool

=head1 SYNOPSIS

  $ fxtran-diffool file1.F90 file2.F90

=head1 DESCRIPTION

Reformat FORTRAN code before invoking a diff utility such as C<kdiff3> or C<meld>.

This is meant to ease difference visualization; for instance, the two following C<CALL>
statements are best diffed if they have a single argument per line:

  CALL ACDRAG (YDMODEL%YRCST, YDMODEL%YRML_PHY_MF, KIDIA, KFDIA, KLON, &
  & NTDRAG, KFLEVG, PAPRS, PAPRSF, PDELP, ZNBVNO, YDMF_PHYS_BASE_STATE%YCPG_PHY%XYB%RDELP, &
  & PU, PV, YDVARS%GEOMETRY%RCORI%T0, YDMF_PHYS_SURF%GSD_VF%PGETRL, ZGWDCS, YDMF_PHYS_SURF%GSD_VF%PVRLAN, &
  & YDMF_PHYS_SURF%GSD_VF%PVRLDI, YDMF_PHYS%OUT%STRDU, YDMF_PHYS%OUT%STRDV, ZTRAJGWD)

  CALL ACDRAG (YDMODEL%YRCST, YDMODEL%YRML_PHY_MF, KIDIA, KFDIA, KLON, &
  & NTDRAG, KFLEVG, PAPRS, PAPRSF, PDELP, ZNBVNO, YDMF_PHYS_BASE_STATE%YCPG_PHY%XYB%RDELP, &
  & PU, PV, YDVARS%GEOMETRY%RCORI%T0, YDMF_PHYS_SURF%GSD_VF%PGETRL, LLFLAG, ZGWDCS, YDMF_PHYS_SURF%GSD_VF%PVRLAN, &
  & YDMF_PHYS_SURF%GSD_VF%PVRLDI, YDMF_PHYS%OUT%STRDU, YDMF_PHYS%OUT%STRDV, ZTRAJGWD)

That is, reformatting the C<CALL> statements make the diff trivial:

  CALL ACDRAG (                                 CALL ACDRAG (
  & YDMODEL%YRCST, &                            & YDMODEL%YRCST, &
  ...
  & PV, &                                       & PV, &
  & YDVARS%GEOMETRY%RCORI%T0, &                 & YDVARS%GEOMETRY%RCORI%T0, &
  & YDMF_PHYS_SURF%GSD_VF%PGETRL, &             & YDMF_PHYS_SURF%GSD_VF%PGETRL, &
  -------------------------------------------   & LLFLAG, &
  & ZGWDCS, &                                   & ZGWDCS, &
  ...
  & YDMF_PHYS%OUT%STRDV, &                      & YDMF_PHYS%OUT%STRDV, &
  & ZTRAJGWD)                                   & ZTRAJGWD)

=head1 AUTHOR

philippe.marguinaud@meteo.fr

=head1 SEE ALSO

L<fxtran-mergetool>, L<Fxtran::Formatter>

=head1 COPYRIGHT

Meteo-France 2025

=cut

use warnings;

use Data::Dumper;
use Getopt::Long;
use FileHandle;
use FindBin qw ($Bin);

use lib "$Bin/../lib";

use strict;

use Fxtran::Common;
use Fxtran::Bt;
use Fxtran::Formatter;
use Fxtran::DiffTool;
use Fxtran::Tool;

my %opts = qw (simplify-associate-blocks 1);
my @opts_f = qw (simplify-associate-blocks help);
my @opts_s = qw (difftool);

&GetOptions
(
  (map { ("$_!", \$opts{$_}) } @opts_f),
  (map { ("$_=s", \$opts{$_}) } @opts_s),
);

if ($opts{help} || (scalar (@ARGV) != 2))
  {
    exec ('perldoc', $0);
  }

unless ($opts{difftool})
  {
    for my $prog (qw (kdiff3 meld vimdiff))
      {
        next unless (&Fxtran::Tool::which ($prog));
        $opts{difftool} = $prog;
        last;
      }
  }

$opts{log} = \&Fxtran::Tool::ll;
$opts{runcommand} = \&Fxtran::Tool::runCommand;

my ($local, $remote) = @ARGV;

'Fxtran::Formatter'->prepareFileForMerging ($_, %opts) 
  for ($local, $remote);

'Fxtran::DiffTool'->diff ($local, $remote, %opts);

exit (0);

